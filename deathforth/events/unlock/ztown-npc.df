cyoa{ road{ regiondef{

cyoa: info-dingus
	"wugh" cyoa-title!
	"Hi, I'm the Info Dingus" text!
;

uses events/unlock/perktrait.df
uses events/unlock/perk1.df
uses events/unlock/perk2.df
uses events/unlock/perk3.df
uses events/unlock/trait1.df
uses events/unlock/gnome.df
uses events/unlock/twitter.df

: challenge-gamemodecheck

	// Increment this 0 by 1 for each game mode with wins
	0 gstats{
		wins-normal		if 1+ then
		wins-long		if 1+ then
		wins-special	if 1+ then
		wins-family		if 1+ then
		wins-specialEX	if 1+ then
		wins-familyEX	if 1+ then
		wins-kepa		if 1+ then
		wins-short		if 1+ then
		wins-marathon	if 1+ then
		wins-deadlier	if 1+ then
		wins-endless	if 1+ then
		wins-opp		if 1+ then
		wins-quickdeath	if 1+ then
		wins-4jerks		if 1+ then
		wins-4jerksEX	if 1+ then
	}
;

cyoa: challenge-statue1
	gstats{ challenge-statue1 } 1 < if
		"CHALLENGE OF MODES I" cyoa-title!
		challenge-gamemodecheck 6 < if
			($ "Beat 6 different game modes to " .. "unlock" rainbold .. " an assortment of " .. "9 NEW HATS" rainbold .. " for the Custom Character Maker!" $) text!
			($ "Game Modes Beaten: " bbold .. challenge-gamemodecheck $) +crtext
		else
			($ "You've beaten 6 different game modes!
			" .. "CHALLENGE COMPLETE!" rainbold $) text!
			goodfx gstats{ 1 to challenge-statue1 } filesave{ save-global-stats }
			"New hats are permanently unlocked. Check them out in the Custom Character Maker!" +crtext
			extrahatcheck
		then
	else
		"CHALLENGE OF MODES I - COMPLETE!" cyoa-title!
		($ "After beating 6 different game modes, you have earned a batch of 9 new hats for the Custom Character Maker! This includes much sought after hats such as:
		" .. "TOILET TANK WITH LID
		HORSE HEAD HOOD
		GOLDEN CROWN" rbold .. "
		AND MORE!
		" fbold $) text!
		extrahatcheck
	then
	choice( "BACK" )choice
;

cyoa: challenge-statue2
	gstats{ challenge-statue1 } 2 < if
		"CHALLENGE OF MODES II" cyoa-title!
		challenge-gamemodecheck 10 < if
			($ "Beat 10 different game modes to " .. "unlock" rainbold .. " an assortment of " .. "6 NEW HATS" rainbold .. " and " .. "8 NEW SHADES" rainbold .. " for the Custom Character Maker!" $) text!
			($ "Game Modes Beaten: " bbold .. challenge-gamemodecheck $) +crtext
		else
			($ "You've beaten 10 different game modes!
			" .. "CHALLENGE COMPLETE!" rainbold $) text!
			goodfx gstats{ 2 to challenge-statue1 } filesave{ save-global-stats }
			"New hats and shades are permanently unlocked. Check them out in the Custom Character Maker!" +crtext
			extrahatcheck extraglassescheck
		then
	else
		"CHALLENGE OF MODES II - COMPLETE!" cyoa-title!
		($ "After beating 10 different game modes, you have earned a batch of 6 new hats and 8 new shades for the Custom Character Maker! This includes much sought after accessories such as:
		" .. "PRINCESS HAT (unisex)
		DUNCE CAP
		DISGUSTING BIRD
		INCREDIBLE FACE DISGUISE
		HI-TECH SHADES" rbold .. "
		AND MORE!
		" fbold $) text!
		extrahatcheck extraglassescheck
	then
	choice( "BACK" )choice
;

cyoa: challenge-statue3
	gstats{ challenge-statue1 } 3 < if
		"CHALLENGE OF MODES III" cyoa-title!
		challenge-gamemodecheck 14 < if
			($ "Beat 14 different game modes to " .. "unlock" rainbold .. " an assortment of " .. "12 NEW HATS" rainbold .. " for the Custom Character Maker!" $) text!
			($ "Game Modes Beaten: " bbold .. challenge-gamemodecheck $) +crtext
		else
			($ "You've beaten 14 different game modes!
			" .. "CHALLENGE COMPLETE!" rainbold $) text!
			goodfx gstats{ 3 to challenge-statue1 } filesave{ save-global-stats }
			"New hats are permanently unlocked. Check them out in the Custom Character Maker!" +crtext
			extrahatcheck
		then
	else
		"CHALLENGE OF MODES III - COMPLETE!" cyoa-title!
		($ "After beating 14 different game modes, you have earned a batch of 12 new hats for the Custom Character Maker! This includes much sought after hats such as:
		" .. "BIG FISH
		TAM O' SHANTER
		HEAD BOOT
		CHEF HAT" rbold .. "
		AND MORE!
		" fbold $) text!
		extrahatcheck
	then
	choice( "BACK" )choice
;

cyoa: challenge-statue4
	gstats{ challenge-statue1 } 4 < if
		"CHALLENGE OF MODES IV" cyoa-title!
		gstats{ best-gamemode-streak 4 } > challenge-gamemodecheck 13 > and if
			($ "You've streaked 5 different game modes!
			" .. "CHALLENGE COMPLETE!" rainbold $) text!
			goodfx gstats{ 4 to challenge-statue1 } filesave{ save-global-stats }
			"New hats are permanently unlocked. Check them out in the Custom Character Maker!" +crtext
			extrahatcheck
		else
			($ "After beating the previous challenges, get a win streak of 5 different game modes to " .. "unlock" rainbold .. " an assortment of " .. "18 NEW HATS" rainbold .. " for the Custom Character Maker!" $) text!
			($ "Game Modes Beaten: " bbold .. challenge-gamemodecheck $) +crtext
			($ "Best Streak of Different Game Modes: " bbold .. gstats{ best-gamemode-streak } $) +crtext
		then
	else
		"CHALLENGE OF MODES IV - COMPLETE!" cyoa-title!
		($ "After streaking 5 different game modes, you have earned a batch of 18 new hats for the Custom Character Maker! This includes much sought after hats such as:
		" .. "GNOMEY CAP
		TIN FOIL HAT
		CARDBOARD MASK
		WARLOCK HAT" rbold .. "
		AND MORE!
		" fbold $) text!
		extrahatcheck
	then
	choice( "BACK" )choice
;

cyoa: jerk-dingus
	"The Jerk Store Called" cyoa-title!
	"YOU'RE A JERK
	OK, THAT'LL BE FIVE DOLLARS!
	
	THANKS FOR SHOPPING
	AT THE JERK STORE" text!
	choice( "THEY'RE ALL OUT OF YOU" )choice
;

cyoa: roaming-capyrun
	"The Roaming Rodent" cyoa-title!
	($ "The rodent thing " .. "(Capybara? Very large gerbil?)" pbold .. " flees when you approach it.
	It runs to the " .. "NORTH-EAST!" rainbold $) text!
	clear-cyoa deleteme
;

cyoa: ugnome-note
	"From the Desk of Unomey" cyoa-title!
	($ "The note reads as follows:" $) cyoa-text! 
	"GET OUT OF MY HOUSE, YOU NERD!" pbold +crtext
;

// : tutorial-repeat 0 saveslot! MAIN_EVENT_NEW main-post-event "events/tutorial.df" $load-region to-mission ;
: tutorial-repeat 0 saveslot! gstats{ 0 to tutorial-done save } sys_reset ;

cyoa: tutorial
	"Tutorial" $upper cyoa-title!
	someicon
	($ "You can repeat the tutorial here!
	This is handy if you missed info on things like dropping weapons, or throwing furniture.
	This option will put you back on the title screen, just hit START to go into the tutorial again." $) cyoa-text! basic-tone
	
		choice( "EXIT" )choice
		choice( "Repeat Tutorial" )choice: tutorial-repeat
;

: dingus-deleted-dialog
	FLASHY "BELETED!" .. $cyoa-ok-dialog "Dingus" $upper cyoa-title!
;

: deletedata-gstats gstats{ gstats-clear } sys_reset 5 0.95 2dup rumble-camera rumble-gui sfx{ rumble } dingus-deleted-dialog ;
: deletedata-custchars filesave{ custchars-clear save-custchars load-custchars } dingus-deleted-dialog sfx{ creepy } ;
: deletedata-both deletedata-custchars deletedata-gstats ;

cyoa-blank: deletedata-gstats-confirm
	"Are you sure you want to delete your stats, perks, and traits? This will also delete things such as game mode progress." text!
	"NOTE: Selecting this option will quit to the title screen." pbold +crtext
	choice( "NO" )choice
	choice( "YES, DELETE THEM" )choice: deletedata-gstats
;

cyoa-blank: deletedata-custchars-confirm
	"Are you sure you want to delete all your custom characters?" text!
	choice( "NO" )choice
	choice( "YES, DELETE THEM" )choice: deletedata-custchars
;

cyoa-blank: deletedata-both-confirm
	"Are you sure you want to delete stats, perks, traits, and your custom characters? This will also delete things such as game mode progress." text!
	"NOTE: Selecting this option will quit to the title screen." pbold +crtext
	choice( "NO" )choice
	choice( "YES, DELETE ALL" )choice: deletedata-both
;

// Do ARE YOU SURE? choices too
cyoa: deletedata
	"Data Clear" $upper cyoa-title!
	"I'M THE DATA DELETION DINGUS
	I WILL DELETE ALL OF YOUR DATA!" rbold text!
	"The skeleton makes rattling noises.
	You can delete your game data here! Though, you probably shouldn't." +crtext
	choice( "EXIT" )choice
	choice( "Custom Characters" )choice: deletedata-custchars-confirm
	choice( "Stats and Perks" )choice: deletedata-gstats-confirm
	choice( "Clear Both" )choice: deletedata-both-confirm
; 

cyoa-blank: freeZP-give-go
	iicon
	"ZOMBO POINTS increased to 99!" rainbold text!
	goodfx gstats{ 99 to zombo-point save }
	
;

cyoa: freeZP-give
	"Zombo Point Giveaway!" cyoa-title! iicon
	"WELL IT'S THE TESTING BRANCH
	SO I CAN GIVE YOU SOME ZOMBO POINTS" bbold text!
	gstats{ zombo-point } 99 < if 
		choice( "Give me those Zombo Points" )choice: freeZP-give-go
	else
		"COME BACK WHEN YOU HAVE LESS THAN 99 ZP!" bbold
	then +crtext
	choice( "Leave for now" )choice
	
;

} } }

map{ regiondef{

: info-dingus
	nomove-npc
	40 lt .character@ .specialtype!
	' infostats lt .cyoa!
;

: tutorial-dingus
	nomove-npc
	10 lt .character@ .specialtype!
	' tutorial lt .cyoa!
;

: delete-dingus
	nomove-npc
	78 lt .character@ .specialtype!
	' deletedata lt .cyoa!
;

: perk-event args( me eventid perk ) THING_EVENT_TIMER <- eventid <> if then;

	<- me .cyoa@ 0= if then; // Just time out, don't display icon anymore

	15 <- me .repeating_timer

	<- me .nearest? if "" <- me .report then;

	// <- me .loot@ dup 0= if drop 1 GREEN "$" .. else looticon then
	// <- me GREEN "11" .. looticon - a ?
	
	<- perk 1 < if
		<- me "?" pbold
	else
		<- me <- perk bbold
	then 1 swap <- me .reportdurnow
;


(*
	EXAMPLE USAGE: Create a new event handler for the specified value

	gstats{ toilets-opened } pe: perk-event-toilet
*)

: pe: token $create last >body ! does> @ execute perk-event ;

: gstat: postpone gstats{ postpone ' postpone } ;

gstats{ ' perk1-stockleft } pe: perk1-stockleft-event
gstat: perk2-stockleft pe: perk2-stockleft-event
gstat: perk3-stockleft pe: perk3-stockleft-event
gstat: trait1-stockleft pe: trait1-stockleft-event
gstat: gnome-stockleft pe: gnome-stockleft-event
gstat: twitter-twitsleft pe: twitter-twitsleft-event

: total-unlocktally gstats{ perk1-unlocktally perk2-unlocktally perk3-unlocktally trait1-unlocktally + + + } ;
// 14 right now without any unlocked, has to be increased if new perks are added that are pre-unlocked

: total-upgradetally gstats{ perk1-upgradetally perk2-upgradetally perk3-upgradetally + + } ;
// 23 with nothing unlocked currently.
// Need to upgrade zombotown.df if either upgradetally or unlocktally changes.

: unlocker-shared
	// new-camper
	nomove-npc
	// 0.01 lt .character@ .ai.wander_chance!
	1 lt .timer!
;

: perk-dingus unlocker-shared
		
	57 lt .character@ .specialtype!
	' perk1-stockleft-event lt .events!
	' perk-dingus lt .cyoa!
;

: perk1-dingus unlocker-shared
		
	12 lt .character@ .specialtype!
	' perk1-stockleft-event lt .events!
	' perk1-dingus lt .cyoa!
;

: perk2-dingus unlocker-shared
		
	25 lt .character@ .specialtype!
	' perk2-stockleft-event lt .events!
	' perk2-dingus lt .cyoa!
;

: perk3-dingus unlocker-shared
		
	46 lt .character@ .specialtype!
	' perk3-stockleft-event lt .events!
	' perk3-dingus lt .cyoa!
;

: trait1-dingus unlocker-shared
		
	24 lt .character@ .specialtype!
	' trait1-stockleft-event lt .events!
	' trait1-dingus lt .cyoa!
;

: jerk-dingus
	// new-camper
	nomove-npc
	21 lt .character@ .specialtype!
	
	' trader-event lt .events!
	1 lt .timer!

	' jerk-dingus lt .cyoa!
;

: challenge-statue1 
	spawn special_objects48_base_id 0x 24 + 2 + lt .spriteid! bbox immovable
	' challenge-statue1 lt .cyoa!
;

: challenge-statue2
	spawn special_objects48_base_id 0x 24 + 1 + lt .spriteid! bbox immovable
	' challenge-statue2 lt .cyoa!
;

: challenge-statue3
	spawn special_objects48_base_id 0x 24 + 3 + lt .spriteid! bbox immovable
	' challenge-statue3 lt .cyoa!
;

: challenge-statue4
	spawn special_objects48_base_id 0x 24 + 0 + lt .spriteid! bbox immovable
	' challenge-statue4 lt .cyoa!
;

: generalinfo-duder
	nomove-npc
	' gameinfo lt .cyoa!
;

: unlockgnome unlocker-shared
	lt .character@ .gnomify2
	' gnome-stockleft-event lt .events!
	' unlockgnome lt .cyoa!	
;

: ugnome-note wrotenote cyoa{ ' ugnome-note cyoa! } ;

: unlockgnome-hut gstats{ ugnome-found } 1 < if unlockgnome else ugnome-note then ;
: capy-gnort args( me )
	<- me ($ "*GNORT*" rbold $) <- me
	.sprayit
	// statmod{ .report } clear-cyoa
	sfx{ sing 500 fx .d! 2.25 fx .pitch! 0.9 fx .q! <- me .adjust-vol } 
;

: capy-chat args( me )
	gstats{ ugnome-found } 0 > if
		<- me capy-gnort
	else
		cyoa{ roaming-capyrun }
	then
	; 

: roaming-capybara
	new-camper lt .character@ .gerbilfy
	// 0.06 lt .character@ .ai.wander_chance!
	' capy-chat lt .cyoa!
;

: hut-capybara
	new-npc lt .character@ .gerbilfy
	' capy-gnort lt .cyoa!
;

: freeZP-dingus
	nomove-npc 
	40 lt .character@ .specialtype!
	
	' trader-event lt .events!
	1 lt .timer!
	' freeZP-give lt .cyoa!
;

: twitter-chicken
	nomove-npc
	lt .character@ .chickenify
	300 angle-colour 2 rgb* lt .clr!

	' twitter-twitsleft-event lt .events!
	1 lt .timer!
	' twitterbird lt .cyoa!
;

} }

// MANDIBLE

cyoa{ regiondef{

cyoa: sycamore-dingus
	"Professor Sycamore" cyoa-title!
	"Don't say timber, it gives me bad memories." pbold text!
	"--- Professor Sycamore
	Creator of: Death Road to Sycamore" +crtext
	"Contributions for MANDIBLE Update: " rbold +crtext
	"Punch Bot and Speed Bot in the Special Mechanical Trading Camp" bbold +crtext
	choice( "Very nice robots" )choice
;

cyoa: modguard-dingus
	"Hut Within a Hut" cyoa-title!
	"I am the guardian of the Contributor Hut!
	Past me are creators that have contributed new content to the Death Road! They are creators of Death Road to Canada mods, from our Discord! " text! "#mod-showcase" pbold +text " and " +text "#deathroad-modding" pbold +text 
	
	"Come inside to read their words of inspiration, and learn what they have added in each update!
	It is a hut within the larger Info Hut!" +crtext
	choice( "Let's check out this hut" )choice
;

} }

map{ regiondef{

: sycamore-dingus
	// new-camper
	nomove-npc
	121 lt .character@ .specialtype!

	' sycamore-dingus lt .cyoa!
;

: modder-dingus
	// new-camper
	nomove-npc
	120 lt .character@ .specialtype!

	' modguard-dingus lt .cyoa!
;


} }
