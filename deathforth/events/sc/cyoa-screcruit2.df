(* uses base.df
uses charfilt.df
uses charstats.df
uses road.df
uses charstatmods.df *)

// Mason, Octodad, Boxer, Mad Scientist, Sumo, Valkyrie, Rambeux

road{

0 value madsci-countdown
0 value mason-countdown
0 value mason-curse

}

cyoa{ road{

cyoa-bridge: ramb-recruit
	ricon recruitee .strengthREV recruitee .fitnessREV recruitee .shootingREV
	team-count 3 > if
		($  rname .. " notices that the team is already full.
		You'll have to boot someone out of the team to make room. You should, since you already gave up the food." $) cyoa-text! -10 trunk-food+ recruit-replace?
	then;
	($ rname  .. " stuffs himself and his minigun into the car. He outright refuses to put " .. "BERTHA" pbold .. " in the trunk, and says no one will ever be able to pry it from him." $) cyoa-text! goodfx
		-10 trunk-food+	recruited
;

cyoa: ramb-test
	"RAMBEUX: FIRST BUTT" cyoa-title!
	char-gen to recruitee
	"Rambeux" recruitee .name!
	ricon
	4 recruitee .morale! recruitee .rambify
	($ Thegroup .. " hears massive barrages of gunfire while approaching a town.
	In the middle of the town is a man holding a minigun. Around him are droves of re-dead undead.
	He says that he is a mercenary for hire, paid up front." $) cyoa-text! basic-tone
	trunk-food 9 > if
		choice( "Recruit him for 10 food" )choice: ramb-recruit
	then
		choice( "Can't spare the food" )choice
;

cyoa-choice: valk-recruit
	ricon
	team-count 3 > if
		($  rname .. " notices that your group is full.
		" .. "Well, someone's gunna have to leave." pbold .. 
		" She throws her hammer and thunks " .. cname .. " right in the skull.
		" .. cname .. " has left the team!" $) cyoa-text! cchar .kill
	else
		($ rname  .. " dramatically raises her hammer. Lightning strikes it!
		The rest of the team is nearly deaf and can't see most colors for the rest of the day." $) cyoa-text! goodfx
	then
		recruitee .strengthREV recruitee .fitnessREV
		recruited
;

cyoa: valk-test
	"Rainbow Bridge to Valhalla" cyoa-title!
	char-gen to recruitee
	someone choosechar
	"Valkyrie" recruitee .name!
	ricon
	4 recruitee .morale! recruitee .valkify
	($ "The clouds part and a rainbow appears. A valkyrie rides a unicorn down the rainbow.
	" .. "You've been slaying tons of stuff. I've been sent here by Thor and crap to help you out on your quest.
	This really isn't a big deal so don't question it." pbold $) cyoa-text! basic-tone
		choice( "Accept her offer" )choice: valk-recruit
		choice( "Reject her offer" )choice
;

cyoa-bridge: sumo-recruit
	ricon recruitee .strengthREV recruitee .fitnessREV
	team-count 3 > if 
		($  rname .. " appreciates the offer at first, but then it becomes really apparent that there's no way in hell that 5 people can fit into the car. Much less 4 people and a sumo wrestler.
		Replace someone:" $) cyoa-text! recruit-replace?
	then;
	($ rname  .. " enters the car. The suspension goes really, really low on one side, but the car still manages to drive down the road." $) cyoa-text! goodfx
		recruited
;

cyoa: sumo-test
	"LET'S SUMO WRESTLE" cyoa-title!
	char-gen to recruitee
	ricon
	4 recruitee .morale! recruitee .sumoify
	($ Thegroup .. " is trying to find supplies and stumbles on a fortified martial arts dojo. Inside is a sumo wrestler.
	He explains that he's surviving, but he wants to travel. Everyone left his town quite awhile ago. Even before then, there wasn't a huge demand for sumo wrestler training on the east coast of the United States.
	" .. "I heard sumo is pretty big in Canada! Bigger than hockey!" pbold $) cyoa-text! basic-tone
		choice( "Recruit him" )choice: sumo-recruit
		choice( "Leave him be" )choice
;

cyoa-choice: madsci-give
	ricon
		(*  Here, take one of my old experiments! It'll work a couple times." pbold .. 
		rname .. " hands you a TELEPORTER MK 1! *)
	($ "Great, this will really further science!
	Here, take one of my old experiments! 
	" pbold .. rname .. " hands you a TELEPORTER MK 1!
	She walks off to continue her voyage of discovery." $) cyoa-text! goodfx
		-20 LOOT_SHOTGUN_SHELL trunk.loot+
		7 WEAPON_SMOKEBOMB trunk.weapon+
;

cyoa-bridge: madsci-recruit
	ricon 2 4 rnd to madsci-countdown recruitee .witsREV recruitee .mechanicREV recruitee .shootingREV
	($  "After talking with the scientist, it turns out her lab is on the way.
	" .. "Thanks. I'll name the next invention after you." pbold $) cyoa-text! goodfx
		team-count 3 > if
			"The group's too big to recruit her! Replace someone:" +crtext
			recruit-replace?
		then; recruited
;

cyoa: madsci-test
	"Donating to Scientific Research" cyoa-title!
	char-gen to recruitee
	ricon
	"Doc" recruitee .name! 4 recruitee .morale! recruitee .madscify
	($ Thegroup .. " is taking a break to check their gear when there's suddenly a bright flash of light!
	A scientist materializes out of nowhere.
	" .. "Crap, now I need to get back to my lab. I could use a ride, or say, 20 shells for my Science Shotgun." pbold $) cyoa-text! basic-tone
		choice( "Offer her a ride" )choice: madsci-recruit
		trunk-shotgun-shell 19 > if
		choice( "Give her 20 Shotgun Shells" )choice: madsci-give
		then
		choice( "Sorry, can't help" )choice
;

// boxer-recruit sometimes gives a stack error that I haven't been able to track down yet
cyoa-bridge: boxer-recruit
	ricon "Contender" recruitee .name! recruitee .strengthREV recruitee .fitnessREV recruitee .attitudeREV
	team-count 3 > if
		($  rname .. " gets pretty agitated about everyone crowding her to ask her to join. She unleashes a big uppercut right to " .. cname .. "'s chin.
		" .. "Yeah, that's a real knockout punch!" pbold .. 
		" The team makes room for the boxer by ditching:" $) cyoa-text!
		sting-- badfx recruit-replace?
	then;
	($ rname  .. " punches her gloves together.
	" .. "Time for some road training!" pbold $) cyoa-text! goodfx
		recruited
;

cyoa: boxer-test
	"The Big Fight" cyoa-title!
	char-gen to recruitee
	someone choosechar
	ricon
	4 recruitee .morale! recruitee .boxerify
	($ Thegroup .. " checks out an abandoned boxing gym and finds a woman there in full boxing gear. She is wailing on a punching bag with blistering speed.
	Her responses in conversation are all non-sequitur, random sayings about boxing." $) cyoa-text! basic-tone
		choice( "Recruit her" )choice: boxer-recruit
		choice( "Leave her be" )choice
;

cyoa-choice: octodad-recruit
	ricon
	team-count 3 > if
		($ "The new recruit turns out to be rather compact, and squeezes into the car!
		" .. rname .. " joins the team!
		Later, while driving down the highway, he somehow flies out the window in a freak accident, along with the weapon and supplies he brought with him.
		" .. rname .. " leaves the team!" $) cyoa-text!
	then;
	($ " It takes " .. rname .. " about 15 minutes to open the car door, get in the car, and put his seatbelt on.
	During this the claymore and supplies get dropped into a nearby toilet and are unrecoverable." $) cyoa-text! badfx
	recruitee .temperREV recruitee .attitudeREV
	recruited
;

: octodadname 1 5 rnd case
	1 of "*blub*" endof
	2 of "*burble*" endof
	3 of "*bluub*" endof
	4 of "New Guy" endof
	5 of "Recruit" endof
endcase ;

cyoa: octodad-test
	"Normal Person" cyoa-title!
	char-gen to recruitee
	ricon
	octodadname recruitee .name!
	4 recruitee .morale! recruitee .octodadify
	($ Thegroup .. " finds a rather nondescript survivor in an abandoned aquarium.
	He seems like a friendly person. On closer inspection, he's carrying a claymore with him and a huge sack of supplies!" $) cyoa-text! basic-tone
		choice( "Recruit him" )choice: octodad-recruit
		choice( "Leave him be" )choice
;

cyoa-choice: mason-recruit
	ricon 2 3 rnd to mason-countdown 1 to mason-curse
	team-count 3 > if
		($ "The team stops at the side of the road to meet the hitchhiker. At first there were too many people to fit a new person into the car, but then " .. cname .. " vanishes off the face of the earth.
		How puzzling!" $) cyoa-text!
		cchar .kill
	else
		($ rname  .. " opens the car door and sits down, saying nothing. Looks like you have a new friend!" $) cyoa-text! goodfx
	then
		recruitee .strengthREV recruitee .fitnessREV
		recruited
;

cyoa: mason-test
	"Meet New Friends" cyoa-title!
	someone choosechar
	char-gen to recruitee
	ricon
	"???" recruitee .name!
	5 recruitee .morale! recruitee .masonify
	($ Thegroup .. " spies a hitchhiker. He is wearing a hockey mask and standing still alongside the road, statue-like, staring off at nothing in particular. In one hand he has a chainsaw, in the other a machete.
	As the car gets closer, the hitchhiker turns his head slightly to gaze right at " .. thegroup .. "." $) cyoa-text! basic-tone
		choice( "Stop for him, whatever" )choice: mason-recruit
		choice( "Drive quite a bit faster" )choice
;



} }


