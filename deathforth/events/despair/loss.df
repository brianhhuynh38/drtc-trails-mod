uses base.df
uses charfilt.df
uses charstats.df
uses road.df

cyoa{ road{

cyoa: loss-lost
	"Lost to Despair" cyoa-title!
	despair-start!
	($ cname .. " has lost all hope, and refuses to keep going. " .. cHe .. " wanders off to an unknown fate." $) cyoa-text! badfx
	($ "What a nerd! Who needs " .. chim .. ", anyway?" $) +crtext
		decruit-bad-silent-takeweapons morale++all
;
	
cyoa: loss-hustle
	despair-start!
	"Everyone Needs to Hustle" cyoa-title!
	($ cname .. " says that " .. "SOMEONE" rbold .. " in the group isn't pulling their weight, and demands that they be kicked out.
	Who should leave the group?" $) cyoa-text! basic-tone
		' victim-kick bully cyoa-team-choice-except
		bully charchoice( "" )choice: bully-all-kick 
;

cyoa-choice: loss-obliv
	($ cname .. " ignores " .. bname .. ". It turns out that " .. che .. " didn't hear " .. bhim .. ".
	" .. bname .. " fumes at this!" $) cyoa-text!
		bully .morale-- badfx
;

cyoa: loss-ultimatum
	despair-start-victim!
	"Ultimatum" cyoa-title!
	team-count 2 > if
		($ cname .. " demands that either " .. vname .. " leaves, or " .. cname .. " does." $)
	else
		($ cname .. " demands that " .. vname .. " leaves. " .. cname .. " says that " .. che .. " will take the car and supplies and continue on to Canada." $)
	then cyoa-text! basic-tone
		victim charchoice( " gets kicked out" )choice: victim-kick
		bully charchoice( " gets kicked out" )choice: bully-kick
		bully-charm?
		bully-sprayit?
		victim oblivious? if
			victim charchoice( " doesn't hear " .. bname )choice: loss-obliv
		then			
;

: sneak-everyone team-count 2 < if notcname else "everyone" then ;

cyoa: loss-quietly
	despair-start-victim!
	"Quietly Disappearing" cyoa-title!
	($ cname .. " sneaks off when " .. everyone .. " is busy.
	" $) cyoa-text! badfx
	cloyalty- if 
		($ bHe .. " is a true weasel who steals the keys for the car, intending to take everything for " .. bhim .. "self." $) +text
	else
		cloyalty+ if
			($ bHe .. " leaves, taking nothing, not even " .. chis .. " personal supplies." $) +text 
			// Formerly: ($ bHe .. " leaves taking only what " .. che .. " was carrying." $) +text 
			loyaltyREV decruit-bad-silent-takeweapons
			($ "There is a note left behind, wishing " .. sneak-everyone .. " luck!" $) +crtext +cr morale++all
			exit
		else
			($ bHe .. " steals as much food as possible before leaving. " $) +text
		then
	then loyaltyREV decruit-bad-silent
	
		most-paranoid if choosechar
		//	cloyalty+ if exit then
			($ "However, " .. cname .. " sensed something was off and followed " .. bname .. ". " $) +crtext 
			
			bully .loyalty low if
				($ "The traitor was prevented from stealing the car and everything in it. " .. bHe .. " flees." $)
			else
				"The thief was surprised, and ended up dropping the food while running away." 
			then +text +cr witsattREV goodfx morale++all
		then;
		
	cloyalty- if
		($ bHe .. " drives off with most of the supplies." rbold $) +crtext car-stolen trunk.killer bandit-gas bandit-food bandit-med bandit-ammo2 morale3-all
	else
		trunk-food 0 = if
			($ "Too bad for " .. bname .. " that there wasn't any food, anyway." $) +crtext victim .morale++ badfx
		then;
		trunk-food 20 < if
			($ "What little the group had was stolen." $) +crtext eat-all badfx
		then;
		($ "The thief took all " .. bhe .. " could carry." $) +crtext -20 LOOT_FOOD trunk.loot+
	then badfx
;

} }


