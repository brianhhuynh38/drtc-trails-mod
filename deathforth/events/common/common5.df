
// EYEBALL Update events

cyoa{ road{ 

// Future Toilet, based on a true story

: common5-toilet-end ($ "The future toilet emits a whirring noise and stops working." $) +crtext goodfx ;

cyoa-choice: common5-toilet-water
	($ "The toilet sprays an enormous amount of water from a hidden bidet. This coats the room in water, but most of it hits " .. cname .. " right in the eyes." $) text!
	morale--
	solo not if ($ Theothersarename .. " delighted by this! " $) +crtext cchar ' .morale2+ foreach-team-char-except then
	cchar .shooting@ 6 < if ($ "But wait! The bidet water was medicated and pleasantly scented! " .. cname .. "'s vision sharpens!" $) +crtext shooting++ then
	most-hurt damage 0 > if ($ Thegroup .. " is fully healed by the medicated bidet water!" $) +crtext ' .fullheal foreach-team-char then
	common5-toilet-end
;

cyoa-blank: common5-toilet-note
	"Song of the Toilet" cyoa-title!
	"The toilet plays beautiful music. The music has no words, yet conveys a strong and haunting message of hope." text!
	morale4+all common5-toilet-end
;

cyoa-choice: common5-toilet-wings
	"The toilet's lid opens and closes rapidly, with big WHOOMP sounds as the lid flings out gusts of air. The lid opens and closes about 30 times in a row, then stops." text!
	($ cname .. " is confused by the purpose of this function." $) +crtext
	common5-toilet-end gstats{ 30 ' toilets-opened +! }
;

cyoa-choice: common5-toilet-butt
	"The toilet seat starts to hum and glow slightly." text!
	($ cname .. " sits down. The eletronic toilet seat gives " .. chim .. " a powerful gluteal" .. "*" pbold .. " massage, as the toilet repeatedly chants in different languages:" $) +crtext
	"THE GLUTES ARE THE KEY TO FITNESS
	THE GLUTES ARE THE KEY TO STRENGTH
	THE GLUTES ARE THE KEY TO POWER
	THE GLUTES ARE THE CENTER OF LIFE" bbold +crtext
	morale++ strength++ fitness++ common5-toilet-end
	"*Vocabulary note: Gluteal means 'butt'" pbold +crtext
;

cyoa: common5-toilet-rare
	"Toilet of the Past's Future" cyoa-title! choosehuman cicon
	($ Thegroup .. " discovers something incredible while exploring a house: An actual working toilet. Everyone has been doing their business in the woods.
	The toilet has a futuristic control panel full of buttons. " .. cname .. " feels compelled to press one, and chooses the button labelled with a picture of:" $) text!
		choice( "Jets of Water?" )choice: common5-toilet-water
		choice( "Musical Notes" )choice: common5-toilet-note
		choice( "Wings Flapping" )choice: common5-toilet-wings
		choice( "A Butt?" )choice: common5-toilet-butt
;

// Long Line

: common5-line-camp ($ "With the day wasted, " .. thegroup .. " has to set up camp." $) +crtext +cr fullcamp 1 calendar-day+ ;
: common5-line-shop ($ "The shop is a trendy upscale grocery store. Most of the fancier items have rotted to mush, but there's still some loot." $) +crtext
	8 12 rnd trunk-food+ 4 6 rnd trunk-medical+ goodfx ;

cyoa-choice: common5-line-pick
	
	cdog? if
		($ cname .. " scampers towards the line and then passes it completely.
		Hours later, " .. cname .. " returns with a stick in " .. chis .. " mouth." $) text!
		1 WEAPON_TREEBRANCH trunk.weapon+
		morale2+ cchar morale--all-except goodfx
		common5-line-camp
	then;
	
	ccat? if
		($ cname .. " rushes into the shop, avoiding the line. " .. cHe .. " emerges carrying a dead rodent, possibly a gerbil?" $) text!
		2 trunk-food+ morale2+ goodfx
	then;
	
	cpet? if
		($ cname .. " lies down for a little break. As an animal, " .. che .. " either doesn't understand the concept of a line, or pretends not to." $) text!
		morale++ goodfx
	then;
	
	cchar oblivious? if
		($ cname .. " skips the line completely. " .. cHe .. " didn't notice there was a line at all, or zombies." $) text!
		witsREV attitudeREV common5-line-shop
	then;
	
	ctemper- if
		cloyalty- if
			($ cname .. " just goes ahead and skips the line completely. " .. cHe .. " does NOT play by the rules!" $) text!
			morale++ aholeREV common5-line-shop
		then;
		($ cname .. " lasts about a minute in the line before losing " .. chis .. " temper!" $) text!
		cstrength+ if
			"WAITING IN LINE IS FOR NERDS!" rbold +crtext
			($ cHe .. " starts speed-punching all the zombies in the line until they explode. There's too many to punch so " .. che .. " gets tired and stumbles away, sweating. What a workout!" $) +crtext
			rageREV fitness++ fitnessREV morale++ goodfx
		then;
		($ cHe .. " starts attacking the zombie in front of " .. chim .. ". The other zombies eventually notice and start attacking. " .. cname .. " is forced to run!" $) +crtext
		sting-- bitefx temperREV 
	then;
	
	ctemper+ if
		($ cname .. " patiently waits in the long line. It moves forward very slowly, but " .. che .. " calmly waits it out." $) text!
		temperREV 
		common5-line-shop common5-line-camp
	then;
	
	($ cname .. " waits in line. It inches forward, to " .. cname .. "'s great irritation." $) text!
	attitude+roll if 
		($ cname .. " tries to think about this positively. " .. cHe .. " manages to endure and get inside the store!" $) +crtext
		attitudeREV morale-- common5-line-shop
	else
		($ "After hours of this, the zombies turn on " .. cname .. " due to " .. chis .. " constant whining. " .. cHe .. " is forced to flee!" $) +crtext
		morale2- sting-- bitefx 
	then temperREV common5-line-camp
;

cyoa-bridge: common5-line-pick4
	"Choose who gets to wait in the zombie line:" text!
	' common5-line-pick cyoa-team-choice 
;

cyoa-blank: common5-line
	"Still Having to Wait in Line" cyoa-title!
	($ Thegroup .. " runs into a horde of zombies. They're completely docile, just waiting outside of a shop in a big single-file line. Every zombie is staring straight ahead." $) text!
		choice( "Ignore this weirdness" )choice
	solo if
		"The shop's sign is faded out and can't be read." +crtext
		soloer charchoice( ": Wait in line" )choice: common5-line-pick
	then;
	"You can send someone to join the line, to solve this mystery. It looks like it could be a long wait." +crtext
	team-count 3 > if
		' common5-line-pick 2 #cyoa-team-choice
		choice( "Pick someone else" )choice: common5-line-pick4
	else
		' common5-line-pick 3 #cyoa-team-choice
	then	
;

// Rock throwing

cyoa-choice: common5-rock-throw
	($ cname .. " is compelled to throw a rock at the hapless zombie." $) text!
	cchar oblivious? if
		($ cHe .. " keeps accidentally throwing the rock in ways that make it return to " .. chim .. ". First it was straight up, then the next few times it hit the fence. This goes on for several hours.
		" .. cname .. " eventually throws the rock over the fence, just barely. " .. cHe .. " is nearly dead from exhaustion by then." $) +crtext
		morale3- sting-- cchar morale--all-except fitness++ strength++ fitnessREV strengthREV witsattREV
		common5-line-camp
	then;
	strength+roll if
		($ cHe .. " puts all " .. chis .. " might into a mighty downward throw. The rock misses the zombie, but obliterates a small garden gnome." $) +crtext
		strengthREV morale++all goodfx
		33 stpick if choosechar ($ cname .. " realizes that this is very offensive to " .. chim .. "!" $) +crtext morale4- badfx then
	then;
	cfitness+ if
		($ cHe .. " performs a throw with perfect form. The rock sails gracefully through the air. It misses the zombie by far, but what a nice throw!" $) +crtext
		fitnessREV morale2+ cchar morale++all-except goodfx
	then;
	($ cHe .. " not only misses, but performs a really embarassing throw. " .. cHe .. " stumbles and nearly falls over the fence and off the cliff." $) +crtext badfx strengthREV fitnessREV morale-- cchar morale++all-except
;

cyoa: common5-rock
	"The Perfect Rock" cyoa-title!
	($ Thegroup .. " finds a perfect throwing rock on the ground. It is next to a low fence guarding a big fall. At the bottom of this drop is a single zombie, standing there, staring at nothing in particular." $) text!
	solohuman if choosehuman
		($ cname ..  " feels a burning desire to throw the rock at this zombie." $) +crtext
		choice( "Leave zombie alone" )choice
		cchar charchoice( ": Throw rock!" )choice: common5-rock-throw
	else
		"It's too good to pass up. Someone MUST throw that rock at this zombie:" +crtext
		' common5-rock-throw cyoa-human-choice
	then
;

// Drag Race

cyoa-choice: common5-drag-race
	vehicle.which@ 1 < if
		($ Thegroup .. " doesn't have a car right now, so it's not a very close race." $) text!
		"I WIN, NERDS!" rbold +crtext morale3-all badfx
	then;
	($ "The race begins! Both drivers " .. "FLOOR IT!" fbold $) text!
	car-speed 2 < if
		($ Thegroup .. "'s slow car had no chance at all in this race." $) +crtext badfx morale3-all
	then;
	car-speed 2 = if
		($ Thegroup .. "'s car is too slow, and is left far behind." $) +crtext badfx morale--all
	then;
	car-speed 3 > if
		"Your car easily wins the race, leaving the other driver in the dust." +crtext goodfx morale4+all
	then;
	car-speed 2 > if
		"Your car barely wins the race. The other driver gives a thumbs up." +crtext morale2+all goodfx
	then;
;

cyoa-choice: common5-drag-ignore
	vehicle.which@ 0 > if
		"YOU CAN'T DO THAT! YOU CAN'T IGNORE A DRAG RACE CHALLENGE!" rbold text!
		car-armor 0 > if
			($ "The driver bumps into " .. thegroup .. "'s car, but just bounces right off the armor.
			The driver's car careens off the road and crashes into a ditch." .. Thegroup .. " is very amused by this." $) +crtext morale3+all goodfx
		then;
		($ "The driver bumps into " .. thegroup .. "'s car, sending it into a spin." $) +crtext
		-1 car-chassis+
		car-chassis 0 > if 
			($ "The car takes minor damage, and " .. thegroup .. " gets hurt." $) +crtext sting--all bitefx
		else
			($ "The battered car couldn't take that stress. It stops running." $) +crtext badfx
		then
	else
		"FINE, BE THAT WAY! I'LL BE BACK WHEN YOU HAVE A CAR!" rbold text!
		($ "The driver " .. "PEELS OUT!" fbold $) +crtext
	then

;

cyoa-choice: common5-drag-ram
	($ "Before the drag race can start, " .. thegroup .. " plows "  .. ghis .. " car into the opponent's car." $) text!
	car-armor 0 > if
		"Your armored car obliterates the fast but flimsy vehicle, the armor doesn't even get scratched." +crtext
	else
		-2 car-chassis+ bitefx
		car-chassis 0 > if
			"Both cars take damage, but the opponent's vehicle takes a lot more. You win the race!" +crtext
		else
			"Both cars are damaged enough to no longer drive. That makes the race a tie!" +crtext
		then 
	then
	($ "The annoying driver runs away, screaming " .. "CHEATER! " rbold .. Thegroup .. " siphons some gas from his wreckage." $) +crtext
	morale++all 30 40 rnd trunk-gas+ goodfx
;

cyoa-choice: common5-drag-rules
	($ cname .. " isn't clear on the rules of drag racing." $) text!
	"THE ONLY RULE OF DRAG RACING... IS THAT THERE ARE NO RULES!" rbold +crtext
	"The driver then goes on to ramble on about a long list of drag racing rules. " +crtext
	most-ahole if choosechar
		($ "During this, " .. cname .. " goes ahead and siphons all the gas from the driver's car." $) +text 80 120 rnd trunk-gas+ aholeREV
	else
		($ "At around rule 60, " .. thegroup .. " slowly drives away while the driver keeps talking." $) +text
	then goodfx
;

cyoa: common5-drag
	"FAAF 15: Gotta Go Fast" cyoa-title! someicon
	($ "A fast and fancy car pulls up to you dramatically. The driver shouts from his window:" $) text!
	"ON THE DEATH ROAD, THE ONLY WAY TO SURVIVE IS TO BE FAST. AND ALSO FURIOUS." rbold +crtext
	"He challenges you to a drag race." +crtext
		choice( "Ignore lunatic" )choice: common5-drag-ignore
	vehicle.which@ 0 > if
		choice( "Ram into his car" )choice: common5-drag-ram
	then
	choice( "Accept challenge" )choice: common5-drag-race
	most-oblivious if
		charchoice( ": Ask for rules" )choice: common5-drag-rules
	then
;

// Radio Station

cyoa-choice: common5-radiodj-siphon
	($ Thegroup .. " siphons the gas out of the generator. There wasn't much left, but every little bit helps.
	The radio station's power shuts down." $) text! 15 30 rnd trunk-gas+ goodfx
;

0 value DJ-PICK

cyoa-choice: common5-radiodj-pick

	// This should be expanded later for lots of possible stuff

	cdog? if
		($ cname .. " says something along the lines of " .. "BARK!" rbold .. " or maybe " .. "BORK?" rbold .. "
		Here's hoping that it confused a lot of people listening to their radios." $) text!
		morale++ goodfx
	then;
	
	cpet? if
		($ cname .. " attacks the microphone!
		The microphone is destroyed!
		" .. cname .. " is very pleased about getting rid of this threat." $) text! bitefx morale2+ strengthREV fitnessREV
	then;
	
	cchar oblivious? if
		($ cname .. " turns the microphone on by accident, then blurts out a lot of embarassing personal facts.
		This includes some useful information to bandits, such as location and current amount of supplies." $) text!
		witsattREV cchar morale2-all-except
	then;
	
	cchar grating? if
		($ cname .. " repeatedly shrieks " .. "COOL IT!" fbold .. " into the microphone.
		After eleven of these shrieks, the radio equipment shorts out and the generator lights on fire." $) text! witsattREV morale++ cchar morale2-all-except 11 to cool-it
	then;
	
	cchar paranoid? if
		($ cname .. " broadcasts a long, nearly breathless rant about how the zombie apocalypse was created by the government.
		The project was codenamed " .. "PROJECT: DINGUS" bbold .. " and the intended goal was to serve as a smokescreen for making jorts" .. 
		"*" rbold .. " illegal.
		FINALLY, the truth has been revealed!" $) text! witsattREV morale3+ cchar morale--all-except
		"* Note: Jorts mean 'jean shorts'." rbold +crtext
	then;
	
	cloyalty- if
		($ cname .. " broadcasts a helpful message:" $) text!
		cchar ahole? if
			"CIVILIZATION HAS ENDED!
			LIE, CHEAT, STEAL GAS FROM TOILETS!
			HOMEWORK SUCKS!!!" rbold +crtext
			"The last line of this tirade is yelled so loud that the microphone emits feedback." +crtext
			aholeREV morale2+
			cchar ' .loyalty-- foreach-team-char-except
		else
			"EVERYONE BUT ME
			... IS A NERD!" rbold +crtext
			loyaltyREV morale++
		then cchar morale--all-except
	then;

	cchar .hiskill-stack depths 0= if 0 else shuffles pops then to DJ-PICK drop
	
	DJ-PICK STAT_MEDICAL = if
		cchar .medical 3 > if
			($ cname .. " knows that " .. chis .. " message can save lives, and desperately rattles off as much practical medical knowledge as " .. che .. " can in a short time.
			Maybe this can help someone." $) text!
			medicalREV morale++
			' .medical++ foreach-team-char
		then;
		cchar .medical 3 < if
			($ cname .. " proudly spouts as much medical knowledge as " .. che .. " can in a short time.
			It's all really bad advice that makes anyone that hears it actually worse off." $) text!
			medicalREV morale2+
			cchar ' .medical-- foreach-team-char-except
		then; medicalREV
	then
	
	DJ-PICK STAT_MECHANICAL = if
		cchar .mechanical 3 > if
			($ cname .. " broadcasts a short impromptu talk show that is a crash course in car repair. It's informative and entertaining.
			Maybe this can help someone." $) text!
			mechanicREV morale++
			' .mechanic++ foreach-team-char
		then;
		cchar .mechanical 3 < if
			($ cname .. " rambles on about repairing sweet cars and 'crafting level 3 lighting-flame-chainsaws' until the power runs out.
			Anyone that hears this actually gets worse at repairing things." $) text!
			mechanicREV morale2+
			cchar ' .mechanic-- foreach-team-char-except
		then; mechanicREV
	then
	
	DJ-PICK STAT_FITNESS = if
		cfitness+ if
			($ cname .. " creates an intense radio calisthenics show. Whoever is listening joins in with the exercises.
			The total combined sweat from this could fill up a bathtub." $) text!
			fitnessREV morale2+
			' .fitness++ foreach-team-char
		then;
	then
	
	DJ-PICK STAT_SHOOTING = if
		cshooting+ if
			($ cname .. " broadcasts some very effective tips and theories on zombie shooting.
			During the show, " .. cHe .. " even comes up with some new trick shots." $) text!
			shootingREV morale++
			' .shooting++ foreach-team-char		
		then;	
	then

	DJ-PICK STAT_STRENGTH = if
		cstrength+ if
			($ cname .. " reads a poem that " .. che .. " wrote about how much " .. che .. " loves being strong.
			The Four Spirits of Swoleness hear this sweet poem, and they confer their blessing!" $) text!
			strengthREV morale++
			' .strength++ foreach-team-char		
		then;	
	then
	
	cchar charming? if
		($ cname .. " can't come up with anything else, so " .. che .. " sings a song.
		It's a great performance, sure to become a new post-zombocalpyic hit single!" $) text! witsattREV morale2+all
	then;
		
	($ cname .. " can't think of anything to say, and stays silent even as the power runs out." $) text! badfx morale--
;

cyoa-bridge: common5-radiodj-pick4
	"Who gets to be the radio DJ?" text!
	' common5-radiodj-pick cyoa-team-choice
;

cyoa: common5-radiodj
	"Death Road Radio" cyoa-title!
	($ Thegroup .. " finds a radio station. The broadcasting equipment is hooked up to a gas generator with a small amount of fuel left. There's enough power to get out one message to anyone that may hear it. " $) text!
		choice( "Siphon generator" )choice: common5-radiodj-siphon
	solo if
		soloer charchoice( ": Send a message" )choice: common5-radiodj-pick
	then;
	"Who should send out the message?" +crtext
	team-count 3 > if
		' common5-radiodj-pick 2 #cyoa-team-choice
		choice( "Pick someone else" )choice: common5-radiodj-pick4
	else
		' common5-radiodj-pick 3 #cyoa-team-choice
	then	
;

// Mysterious Radio Signal

: common5-signal-win
	"The message warns that the base has been abandoned, but they hid a stash of ammo to help other survivors.
	The stash turns out to be nearby!" +crtext morale++all ammosearch2
	solohuman if witsREV else ' .witsREV foreach-team-char // wits-total is for the whole team instead of just humans, oddly... may change this later
		wits-total 17 > if
			"The team decodes an additional ultra-secret message:" +crtext
			char-gen to recruitee ricon
			($ "We also ditched " .. recruitee .name@ .. ", " .. rhe .. " was a real nerd.
			You can recruit " .. rhim .. " if you are desperate." $) bbold bbold +crtext
			recruit-yesno
		then
	then
;

cyoa-blank: common5-signal-decode
	solohuman if
		choosehuman ($ cname .. " tries to figure the code out by " .. chim .. "self..." $) text!
		cwits+ if common5-signal-win then;
	else
		"The team puts their heads together to figure out the code..." text!
		wits-total 10 > if common5-signal-win then;
	then
	"The message says something like " +crtext "DRINK MORE OVALTUMS" bbold +text
	"It's just nonsense! What a waste of time!" +crtext morale--all
;

cyoa-blank: common5-signal-drive
	($ Thegroup .. " attempts to drive to the signal by listening for the radio broadcast as it gets weaker or stronger. This takes quite a bit of time." $) text!
	car-mpg 0 = if
		"Funny enough, this car takes no gas." +crtext
		goodfx
	else		
		trunk-gas 70 car-mpg * >= if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			bandit-gas
		then
	then +cr fullcamp heal-event
	($ gThey .. " find" .. g1s .. " a prepper bunker that is still playing the message. Everyone left in a hurry, leaving behind a stash of supplies.
	" $) +crtext homesearch2 ammosearch2
;

cyoa-choice: common5-signal-gov
	"THIS IS AN OBVIOUS TRAP SET BY THE GOVERNMENT.
	THE SECRET ZOMBIE GOVERNMENT!" pbold text!
	($ gHe .. " wastes no time in " .. "PEELING OUT" fbold .. " of there!
	" .. cname .. " is quite relieved to have escaped." $) +crtext morale2+ witsattREV
;

cyoa: common5-signal
	"Mysterious Radio Signal" cyoa-title!
	($ Thegroup .. " finds a strange repeating message on the radio. It's a series of short bursts of music, beeps, spoken numbers, and haunting slide whistle noises. There's also a kazoo in there.
	What could it all mean?" $) text!
		choice( "Ignore it" )choice
		choice( "Decipher message" )choice: common5-signal-decode
		choice( "Drive to signal" )choice: common5-signal-drive
		most-paranoid if
			charchoice( ": THIS IS A TRAP!" )choice: common5-signal-gov
		then
;

// Repair Shop repair

cyoa-choice: common5-repair-pick
	($ cname .. " spends a day trying to fix something in the junk pile... " $) text!
	cchar .mechanical@ case
		0 of ($ cHe .. " accidentally gets a lot of cuts, scrapes, and bruises. " .. cHe .. " learns about repair, the hard way." $) +text endof
		1 of ($ cHe .. " takes a broken tennis racket and turned it into a " .. "Level 2 Ultra Ergonomic Tennis Racket of the Whale" bbold .. ". This is exactly the same as a regular tennis racket." $) +text 1 WEAPON_TENNIS trunk.weapon+ endof
		2 of ($ cHe .. " turns a useless Rusted Machete into a useful or at least usable " .. "Rusty Machete" bbold .. ". It's a clear improvement." $) +text 1 WEAPON_MACHETE1 trunk.weapon+ endof
		3 of ($ cHe .. " reinforced a splintered bat with nails and metal to make a " .. "Spiked Bat" bbold .. "! It's great until it breaks!" $) +text 1 WEAPON_SPIKEDBAT trunk.weapon+ endof
		4 of ($ cHe .. " sharpens a dull " .. "Branch Trimmer" bbold .. "! It's a decent if slightly awkward weapon that won't break on you!" $) +text 1 WEAPON_BRANCHTRIM trunk.weapon+ endof // This was a sturdy machete until I chickened out
		5 of ($ cHe .. " gets a broken " .. "Leaf Blower" bbold .. " working again! Send zombies flying like leaves!" $) +text 1 WEAPON_BLOWER trunk.weapon+ endof
		6 of ($ cHe .. " restores a faulty " .. "Weed Whacker" bbold .. "! It's like a slightly wimpy son to the chainsaw! To make up for the lack of power, it's much more gas efficient." $) +text 1 WEAPON_WHACKER trunk.weapon+ endof
	endcase mechanicREV mechanic++
	+cr fullcamp heal-event
;

cyoa-bridge: common5-repair-pick4
	"Who should spend a day repairing junk?" text!
	' common5-repair-pick cyoa-human-choice

;

cyoa: common5-repair
	"Repair Shop Repair" cyoa-title!
	($ Thegroup .. " finds a repair shop. There's a tiny bit of gas, plus a lot of broken garbage. There could be something good here, if you know how to fix it." $) text!
	10 16 rnd trunk-gas+
		choice( "Ignore junk" )choice
	solohuman if choosehuman cchar charchoice( ": Repair junk" )choice: common5-repair-pick "You could spend a day repairing things in here." +crtext then;
	"Pick someone to spend a day repairing junk:" +crtext
	numhums 3 > if
		' common5-repair-pick 2 #cyoa-human-choice
		choice( "Pick someone else" )choice: common5-repair-pick4
	else
		' common5-repair-pick cyoa-human-choice
	then
;

// Health Food Store healing

0 stack
	"thinks the food is fine." shove
	"doesn't mind the horrid food." shove
	"has a positive attitude about this slop!" shove
	"is trying to keep an open mind." shove
	"pretends that it's junk food instead." shove
	"successfully ignores the food." shove
	"thinks about curry." shove
	"focuses on the health benefits." shove
	"tries to block out the smell." shove
	"has eaten worse food, possibly." shove
	"mentally blocks out the disgust." shove
	"resists feeling any emotion right now!" shove
value common5-healthfood-line-base
common5-healthfood-line-base shuffles var common5-healthfood-line

: common5-healthfood-say
	common5-healthfood-line depths 0= if common5-healthfood-line-base shuffles to common5-healthfood-line then pops nip
;

: common5-healthfood-stat args( @ )
	<- @ .cattitude+ if
		($ <- @ .colourname .. " " .. common5-healthfood-say $) +crtext 
	else
		<- @ .cattitude- if
			<- @ .morale2-
		else
			<- @ .morale--
		then
	then <- @ .attitudeREV <- @ .health++
	
;

cyoa-blank: common5-healthfood-go
 ($ Thegroup .. " loots the gross health food, picking what " .. gthey .. " can live with.
  This turns out to be not that much of the food in total." $) text!
  4 6 rnd trunk-food+
  ' common5-healthfood-stat foreach-team-char
  ($ Thegroup .. " feels healthier just looking at the gross food!" $) +crtext
;

cyoa: common5-healthfood
	"Health Food Havoc" cyoa-title!
	($ Thegroup .. " finds a health food store. It seems like no one bothered to loot it because the food was disgusting.
	Most of the food seems to be rotting anyway. Or maybe it's not, it could be that the food is supposed to look and smell like this." $) text!
		choice( "Ignore for morale reasons" )choice: morale++all
		choice( "Loot the gross food" )choice: common5-healthfood-go
;

// The Swole-O-Flex

0 value common5-swoloflex@

: common5-swoloflex-go
	common5-swoloflex@ 1 < if
		"The machine starts its patented automated total-body bodybuilding regiment..." text!
		($ cname .. "'s body makes audible snapping and crunching sounds! How horrifying!" $) rbold +crtext
		strength++ ouch-- 1 to common5-swoloflex@ bitefx
	else
		($ "The machine clamps " .. cname .. " in place for its innovative gymnastics simulator..." $) text!
		($ "A high-tension cable snaps free and whips around! There's no time to react to it!" $) rbold +crtext
		fitness++ ouch-- 0 to common5-swoloflex@ bitefx
	then
;

cyoa-choice: common5-swoloflex-again
	($ cname .. " finishes " .. chis .. " workouts, no matter the cost!" $) text!
	common5-swoloflex-go
	dead? if
		($ "A little speaker on the machine says " .. "NO PAIN NO GAIN!" rbold $) +crtext
	then;
;

cyoa-bridge: common5-swoloflex-pick
	($ cname .. " uses the innovative Swolo-O-Flex personal exercise system..." $) text!
	common5-swoloflex-go
	dead? if
		($ "A little speaker on the machine says " .. "NO REFUNDS!" rbold $) +crtext
	then;
		choice( "Avoid further injury" )choice
		choice( "Finish workout!" )choice: common5-swoloflex-again
;

cyoa-bridge: common5-swoloflex-pick4
	"Who should get maimed or killed by the Swole-O-Flex?" rbold text!
	' common5-swoloflex-pick cyoa-human-choice
;

cyoa: common5-swoloflex
	"Swole-O-Flex" cyoa-title!
	0 1 rnd to common5-swoloflex@
	($ Thegroup .. " stumbles upon a " .. "Swole-O-Flex Home Strength and Fitness Machine [tm]" bbold .. "!
		You don't see these much anymore after all the lawsuits against it and the government ordered recalls." $) text!
		// The company that made it went out of business shortly before the zombocalypse hit. - Extra text
		choice( "Stay away from it" )choice
	solohuman if choosehuman cchar charchoice( ": Use Swole-O-Flex" )choice: common5-swoloflex-pick "Using this will hurt." rbold +crtext then;
	"Pick someone to use Swole-O-Flex and probably get killed:" rbold +crtext
	numhums 3 > if
		' common5-swoloflex-pick 2 #cyoa-human-choice
		choice( "Pick someone else" )choice: common5-swoloflex-pick4
	else
		' common5-swoloflex-pick cyoa-human-choice
	then
;

// Gnome Challenge Conduct Rare

defer common5-gnomerepeat
: common5-gnome-back choice( "BACK" )choice: common5-gnomerepeat ;

cyoa-choice: common5-gnome-no
	"All the challenges sound awful." text! ricon
	($ "The gnome shrieks " .. "WHATEVER, NERDS!" rbold .. " and then waddles away in a huff." $) +crtext
	($ Thegroup .. " is relieved that this is all over." $) +crtext morale++all goodfx
;

0 stack
	" chuckles quietly." shove
	" doesn't feel much different, just kind of smug." shove
	" shrugs, rolls eyes." shove
	" tries not to laugh." shove
value common5-gnome-line-base
common5-gnome-line-base shuffles var common5-gnome-line

: common5-gnome-say	common5-gnome-line depths 0= if common5-gnome-line-base shuffles to common5-gnome-line then pops nip ;

: common5-gnome-morale-stat args( @ )
	<- @ .trait@ "Civilized" $= if
		($ <- @ .colourname .. common5-gnome-say $) +crtext
		<- @ .morale3+
	then
	<- @ .shooting++ <- @ .mechanic++ <- @ .medical++ <- @ .strength++ <- @ .fitness++
	<- @ .vitality++ -3 STAT_MORALE <- @ .bonus!
;

cyoa-blank: common5-gnome-morale-go
	"CAN YOU SURVIVE THE SADNESS?" rbold text! goodfx
	' common5-gnome-morale-stat foreach-team-char
;

cyoa-bridge: common5-gnome-morale
	"BE CURSED BY ULTIMATE EMO SADNESS!" rbold text! ricon
	($ "Choosing this will make " .. thegroup .. "'s maximum morale be 3 lower than normal. In exchange, " .. gthey .. " will get:" $) +crtext
	"+ An extra point added to maximum health!
	+ One point in all skills, up to each maximum!" bbold +crtext
	common5-gnome-back
	choice( "Accept Challenge!" )choice: common5-gnome-morale-go
;

: common5-gnome-melee1 "Got the WHAMMER!" bbold +crtext 1 WEAPON_MEGASPIKEDBAT trunk.weapon+ ;
: common5-gnome-melee2 "Got a STRONG CLEAVER!" bbold +crtext 1 WEAPON_SCLEAVER trunk.weapon+ ;
: common5-gnome-melee3 "Got a WOODEN CLUB!" bbold +crtext 1 WEAPON_WOODCLUB trunk.weapon+ ;
: common5-gnome-melee4 "Got a MIGHTY CLAYMORE!" fbold +crtext 1 WEAPON_CLAYMORE trunk.weapon+ ;
: common5-gnome-melee5 "Got a HALBERD!" fbold +crtext 1 WEAPON_HALBERD trunk.weapon+ ;

0 stack
	' common5-gnome-melee1 shove
	' common5-gnome-melee2 shove
	' common5-gnome-melee3 shove
	' common5-gnome-melee4 shove
	' common5-gnome-melee5 shove
value common5-gnome-meleewep-base
common5-gnome-meleewep-base shuffles var common5-gnome-meleewep

: common5-gnome-givemelee common5-gnome-meleewep depths 0= if common5-gnome-meleewep-base shuffles to common5-gnome-meleewep then pops nip execute ;

cyoa-blank: common5-gnome-shoot-go
	"NO MORE GUNS OR CHAINSAWS! ENJOY YOUR CHALLENGE!" rbold text! goodfx
	' .nocanshoot foreach-team-char
	team-count-alive for common5-gnome-givemelee next
;

cyoa-choice: common5-gnome-shoot
	"GUNS AND CHAINSAWS ARE TOO LOUD! I HATE THEM!" rbold text! ricon
	($ "Choosing this will make " .. thegroup .. " no longer capable of shooting guns or using chainsaws or similar weapons. In exchange, you will get " $) +crtext
	"one random high quality melee weapon for each member of your group!
	Possible weapons: Wooden Club, The Whammer, Sturdy Cleaver, Mighty Claymore, Halberd" bbold +text
	common5-gnome-back
	choice( "Accept Challenge!" )choice: common5-gnome-shoot-go
;

: common5-gnome-firearm1 "Got an UZI!" bbold +crtext 1 WEAPON_UZI trunk.weapon+ 80 trunk-bullet+ ;
: common5-gnome-firearm2 "Got an ASSAULT RIFLE!" bbold +crtext 1 WEAPON_AK47 trunk.weapon+ 50 trunk-rifle-shell+ ;
: common5-gnome-firearm3 "Got a MINIGUN!" fbold +crtext 1 WEAPON_MINIGUN trunk.weapon+ 100 trunk-rifle-shell+ ;
: common5-gnome-firearm4 "Got an AUTOMATIC SHOTGUN!" bbold +crtext 1 WEAPON_AUTOSHOT trunk.weapon+ 30 trunk-shotgun-shell+ ;
: common5-gnome-firearm5 "Got a RIFLED SLUG SHOTGUN!" bbold +crtext 1 WEAPON_SLUG trunk.weapon+ 30 trunk-shotgun-shell+ ;

0 stack
	' common5-gnome-firearm1 shove
	' common5-gnome-firearm2 shove
	' common5-gnome-firearm3 shove
	' common5-gnome-firearm4 shove
	' common5-gnome-firearm5 shove
value common5-gnome-firearm-base
common5-gnome-firearm-base shuffles var common5-gnome-firearm

: common5-gnome-givefirearm common5-gnome-firearm depths 0= if common5-gnome-firearm-base shuffles to common5-gnome-firearm then pops nip execute ;

cyoa-blank: common5-gnome-melee-go
	"MELEE WEAPONS ARE FORBIDDEN FOREVER! GOOD LUCK!" rbold text! goodfx
	' .atkspecial foreach-team-char
	team-count-alive for common5-gnome-givefirearm next
;

cyoa-choice: common5-gnome-melee
	"WHO NEEDS MELEE WEAPONS WHEN YOU HAVE FRIENDSHIP?" rbold text! ricon
	($ "Choosing this will make " .. thegroup .. " no longer capable of melee attacks. In exchange, you will get " $) +crtext
	"one random high quality firearm for each member of your group, with ammo!
	Possible guns: Uzi, Assault Rifle, Automatic Shotgun, Slug Shotgun, Minigun" bbold +text
	common5-gnome-back
	choice( "Accept Challenge!" )choice: common5-gnome-melee-go
;

cyoa: common5-gnome-rare
	"Challenge Mode" cyoa-title! char-gen to recruitee ricon recruitee .gnomify2
	($ "During a moment's peace, a gnome shows up and starts shrieking.
	" .. "I'M CNOMEY THE CHALLENGE CONDUCT GNOME!" rbold .. "
	It gives you the choice of an extra challenge, in exchange for a " .. "FABULOUS REWARD!" rainbold $) text!
		choice( "No thanks" )choice: common5-gnome-no
		choice( "No Melee Attacks" )choice: common5-gnome-melee
		choice( "No Shooting/Chainsaw" )choice: common5-gnome-shoot
		choice( "Max Morale Penalty" )choice: common5-gnome-morale
;

' common5-gnome-rare to common5-gnomerepeat

// Cursed Ancient Chest

0 value common5-chest@

cyoa-choice: common5-chest-rare-

;

: common5-chest-weapon
	common5-chest@ case
	0 of "Pirate Cutlass" endof
	1 of "Mighty Claymore" endof
	2 of "Sturdy Cleaver" endof
	endcase rbold
;

: common5-chest-weaponpick
	common5-chest@ case
		0 of WEAPON_CUTLASS endof
		1 of WEAPON_CLAYMORE endof
		2 of WEAPON_SCLEAVER endof
	endcase cchar
;

cyoa-choice: common5-chest-pick
	($ cname .. " picks up the " .. common5-chest-weapon .. "!
	It fuses to " .. chis .. " hands, preventing the use of any other weapon!" $) text!
	ctakeweapons 1 cchar .carrying.slot_count!
	common5-chest-weaponpick .weapona! 1 cchar .weapon_nodropa!
	cchar .trait@ "Specialist" $= if
		($ cname .. " feels naturally familiar with this weapon somehow, and also feels just a little smug." $) +crtext strength++ fitness++ morale++ goodfx
	then; goblin-laugh
;

cyoa-bridge: common5-chest-pick4
	solohuman if
		choosehuman common5-chest-pick
	then;
	($ "Who should be given the incredibly cursed " .. common5-chest-weapon .. "?" $) text!
	"WARNING: The weapon will be locked to the character, and the character will be reduced to one carrying slot!" rbold +crtext
	' common5-chest-pick cyoa-human-choice
;

cyoa-choice: common5-chest-book
	solohuman if
		choosehuman ($ cname .. " reads the " .. "Dingusnomicon!" rbold $) text!
	else
		($ "Everyone takes turns reading the " .. "Dingusnomicon!" rbold $) text!	
	then
	($ "It's an evil, ancient book covered in eyes and mouths. It keeps doing little bites and also shouts " .. "'nerd'" fbold .. " in a thousand different forgotten languages." $) +crtext
	morale--all sting--all bitefx
	"The book imparts its FORBIDDEN KNOWLEDGE!" bbold +crtext
	' rewardevent-random foreach-human-char
	choosehuman cchar rewardevent-random
;

0 value common5-lootweapon@

: common5-lootweapon-name
	common5-lootweapon@ case
		0 of "Knight Sword" endof
		1 of "Bardiche" endof
		2 of "Medieval Axe" endof
		3 of "Sturdy Machete" endof
		4 of "Rapier" endof
		5 of "Halberd" endof
		6 of "Morning Star" endof
	endcase rbold
;

: common5-lootweapon-pick
	1 common5-lootweapon@ case
		0 of WEAPON_KNIGHT endof
		1 of WEAPON_BARDICHE endof
		2 of WEAPON_MEDIEVAL endof
		3 of WEAPON_MACHETE2 endof
		4 of WEAPON_RAPIER endof
		5 of WEAPON_HALBERD endof
		6 of WEAPON_MORNING endof
	endcase trunk.weapon+
;

cyoa-choice: common5-chest-loot
	0 6 rnd to common5-lootweapon@
	($ Thegroup .. " finds a cache of very old weapons! While sorting through unusable junk, " .. gthey .. " run" .. g1s .. " into a lot of problems with splinters and tiny, bitey spiders." $) text!
	morale--all sting--all bitefx
	($ gThey .. " take some useful weapons, including a " .. common5-lootweapon-name .. "!" $) +crtext
	common5-lootweapon-pick
	1 30 %chance if WEAPON_WOODCLUB else 50 %chance if WEAPON_GAUNTLET else WEAPON_DAGGER then then trunk.weapon+ 1 WEAPON_DAGGER trunk.weapon+ 
;

cyoa-choice: common5-chest-basket
	($ Thegroup .. " found a Goblin Gift Basket!" $) text!
	homesearch2 ammosearch2 morale++all
	($ "The gift basket is mildly cursed! Now " .. cname .. " feels obligated to buy a gift for the unseen and possibly nonexistent goblins!" $) +crtext goblin-laugh
;

: common5-chest-flash
	common5-chest@ case
	0 of "Pirate Cutlass" endof // CUTLASS
	1 of "Mighty Claymore" endof // CLAYMORE
	2 of "Sturdy Cleaver" endof // SCLEAVER
	endcase fbold
;

cyoa: common5-chest-rare
	"Cursed Treasure" cyoa-title! someicon
	0 2 rnd to common5-chest@
	($ Thegroup .. " looks for supplies in a creepy cabin and uncovers a hidden basement with an ancient treasure!" $) text!
	"Warning: Red choices have a penalty of morale loss and nonfatal damage! The" rbold +crtext " flashing " fbold +text "choice will set the character you give the weapon to down to one locked slot!" rbold +text
		choice( "Goblin Gift Basket" )choice: common5-chest-basket
		choice( "The Dingusnomicon" rbold )choice: common5-chest-book
		choice( "Long Lost Weapons" rbold )choice: common5-chest-loot
		choice( common5-chest-flash )choice: common5-chest-pick4
;

// Hand Replacement

cyoa-choice: common5-hand-pick
	($ nursename .. " attempts to prepare the room for surgery." $) text!
	teamnurse .medicalREV
	($ cname .. " loses " .. chis .. " boring old hand and gets a " .. "Chainsaw Hand!" rainbold .. " This modified chainsaw is more efficient!" $) +crtext
	ctakeweapons WEAPON_CHAINHAND cchar .weapona! 1 cchar .weapon_nodropa!
	cchar .trait@ "Specialist" $= if
		($ cname .. " feels INCREDIBLY smug!" rainbold $) +crtext
		2 cchar .carrying.slot_count! morale4+
	then
	teamnurse .cmedical+ teamnurse .medical statroll or if
		"The surgery went well!" +crtext goodfx
	else
		"The surgery was very clumsy." +crtext strength-- sting-- badfx
	then
	1 WEAPON_DSHOTGUN trunk.weapon+ // Putting this here just so that it can be given last in -loot
	choice( "Groovy" )choice
;

cyoa-bridge: common5-hand-pick4
	"Who do you want to give a chainsaw hand to?" text!
	' common5-hand-pick cyoa-human-choice
;

cyoa-choice: common5-hand-loot
	($ "Instead of going through with the hand replacement surgery, " .. thegroup .. " loots all the medical supplies in the room. There's even a Healing Spray!" $) text!
		9 trunk-medical+ 3 WEAPON_HEALSPRAY trunk.weapon+ goodfx
	mechanicest choosechar cmechanic+ if
		($ cname .. " manages to convert the chainsaw hand into a normal wieldable chainsaw!" $) +crtext
		mechanicREV 1 WEAPON_CHAINSAW trunk.weapon+ mechanicREV
	else
		"The chainsaw hand was designed for arm-mounted use only, so it is left behind." +crtext
	then 1 WEAPON_DSHOTGUN trunk.weapon+
;

cyoa: common5-hand-rare
	"Hand Upgrade" cyoa-title!
	medicalest to teamnurse
	($ Thegroup .. " finds a makeshift surgery room. In one corner is a skeleton with a double barrel shotgun and prosthetic chainsaw hand, both empty. 
	Chainsaw hand? That seems like a good idea!" $) text!
		choice( "Loot supplies instead" )choice: common5-hand-loot
	solohuman if
		choosehuman cchar charchoice( ": Replace Hand" )choice: common5-hand-pick 
		"Chainsaws are better than hands." +crtext then;
	"Replace someone's hand with a chainsaw:" +crtext
	numhums 3 > if
		' common5-hand-pick 2 #cyoa-human-choice
		choice( "Pick someone else" )choice: common5-hand-pick4
	else
		' common5-hand-pick cyoa-human-choice
	then
;

} }

// Blank
(*
cyoa-choice: common5--

;

cyoa: common5-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)
