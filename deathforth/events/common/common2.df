
cyoa{ road{ 

: camp-pic
	50 %chance if
		image-camp-1
	else
		image-camp-2
	then	
;

// Rares

cyoa-choice: common2-rare-tp-share
	someicon
	solo if
		($ cname .. " realizes that this roll of toilet paper is a treasure that must not be given away." $) text! morale4+ goodfx 1 WEAPON_CARDBOARD trunk.weapon+
		zombop++
	then;
	"The team decides to try to share the roll of toilet paper." text!
	loyalty-average 2 < if
		"It doesn't take them long to be erupt into a huge fight! The situation descends into an all-out brawl. The toilet paper ends up getting torn apart by accident!" +crtext morale3-all badfx
	else
		loyalty-average 4 < if
			"There's some inevitable fighting from having to share the TP, but the group manages to overcome their differences." +crtext morale2+all goodfx
		else
			"The group joins forces around the toilet paper, mutually protecting it. Even after the TP is gone, the cardboard tube becomes a symbol for the group's unity." +crtext morale4+all goodfx
		then
	then
	' .loyaltyREV foreach-team-char 1 WEAPON_CARDBOARD trunk.weapon+
	zombop++
;

cyoa-choice: common2-rare-tp-trade
	someicon
	($ Thegroup .. " decides to sell the roll of toilet paper as soon as possible. It's not worth carrying something THAT valuable around!" $) text!
		50 %chance if
			"A settlement buys the roll of TP for 20 food! Not bad!" +crtext 20 trunk-food+
		else
			50 %chance if
				"A shrewd merchant buys the TP for 12 food. Can't complain!" +crtext 12 trunk-food+
			else
				($ "A " .. "VERY" rbold .. " interested party buys the TP for " .. "30 food!" fbold $) +crtext 30 trunk-food+
			then
		then goodfx morale++all
;

cyoa: common2-rare-tp
	"More Valuable Than Gold" cyoa-title!
	($ Thegroup .. " finds a " .. "PRISTINE ROLL OF TOILET PAPER" fbold .. " while scavenging. Toilet paper was the first thing to run out after the apocalypse. There's still tons of canned food and ammo left in America. Decent butt paper was used up fast.
	This is the rarest resource possible. CHOOSE WISELY:" $) text!
		solo if
			choice( "Use this precious gift" )choice: common2-rare-tp-share
		else
			choice( "Share this precious gift" )choice: common2-rare-tp-share
		then
		choice( "Trade it away ASAP" )choice: common2-rare-tp-trade
;

cyoa-choice: common2-rare-zombmaster-run
	image-creepy-girl
	($ "The " .. "MASTER OF ZOMBIES" rbold .. " sets up a little obstacle course and teaches " .. thegroup .. " how to run in a more efficient, less embarassing fashion!" $) text! goodfx
	' .dexterity++ foreach-team-char
;

: common2-rare-zomb-workout args( @ )
	<- @ .strengthREV <- @ .fitnessREV <- @ .strength++ <- @ .fitness++
;

cyoa-choice: common2-rare-zombmaster-fight
	image-creepy-girl
	($ "The " .. "MASTER OF ZOMBIES" rbold .. " enlightens " .. thegroup .. " on melee weaponry with some tips and some " .. "VERY DANGEROUS TRAINING." rbold $) text! goodfx bitefx
	' common2-rare-zomb-workout foreach-team-char sting--all
;

cyoa-choice: common2-rare-zombmaster-shoot
	image-creepy-girl
	($ "The " .. "MASTER OF ZOMBIES" rbold .. " gives " .. thegroup .. " a great lesson on proper shooting! She gives a homework assignment to do some real zombie hunting, and provides a bunch of ammo and a rifle!" $) text! goodfx ' .shootingREV foreach-team-char ' .shooting++ foreach-team-char
	60 trunk-bullet+ 40 trunk-rifle-shell+ 30 trunk-shotgun-shell+ 1 WEAPON_RIFLE trunk.weapon+
;

cyoa-choice: common2-rare-zombmaster-tough
	image-creepy-girl
	($ "The " .. "MASTER OF ZOMBIES" rbold .. " shows " .. thegroup .. " how to toughen " .. ghis .. " skin up against zombie bites! This process involves her beating " .. ghim .. " up." $) text! goodfx bitefx
	' .vitality++ foreach-team-char sting--all
;

cyoa: common2-rare-zombmaster
	image-creepy-girl "The Zombie Master" cyoa-title!
	($ Thegroup .. " runs into a strange woman wearing goggles, a scarf, and a cape. She says she's the " .. "MASTER OF ZOMBIES" rbold .. " and that she will teach one of her secret zombie tips as a service to humanity." $) text! goodfx
		choice( "Zombie shooting lesson" )choice: common2-rare-zombmaster-shoot
		choice( "Just run away constantly" )choice: common2-rare-zombmaster-run
		choice( "How to fight with melee" )choice: common2-rare-zombmaster-fight
		choice( "Toughen up against bites" )choice: common2-rare-zombmaster-tough
;

// END rares

// Man trapped under cardboard thievery

0 value common2-cboardtype

: cboardprize
	common2-cboardtype case
		1 of "NIGHTSTICK" endof
		2 of "SHOTGUN" endof
		3 of "RIFLE" endof
		4 of "DOLT45 REVOLVER" endof
		5 of "FIRE AXE" endof
		6 of "SNUBNOSE REVOLVER" endof
	endcase
;

: cboardget
	common2-cboardtype case
		1 of 1 WEAPON_NIGHTSTICK endof
		2 of 8 trunk-shotgun-shell+ 1 WEAPON_SHOTGUN endof
		3 of 12 trunk-rifle-shell+ 1 WEAPON_RIFLE endof
		4 of 6 trunk-bullet+ 1 WEAPON_DOLT endof
		5 of 1 WEAPON_FIRE endof
		6 of 6 trunk-bullet+ 1 WEAPON_SNUBNOSE endof
	endcase
;

cyoa-choice: common2-cboard-steal
	($ cname .. " steals the helpless man's " .. cboardprize rbold .. "." $) text! badfx
	cloyalty- if
		($ cHe .. " feels pretty smug about this." $) +crtext morale++
	else
		cloyalty+ if
			($ cHe .. " feels incredibly guilty about this!" $) +crtext morale3-
		else
			($ cHe .. " feels guilty about this." $) +crtext morale--
		then
	then loyaltyREV
	($ "Got a " rbold .. cboardprize rbold $) +crtext cboardget trunk.weapon+
;

: cboard-help-check args( @ )
	<- @ .cloyalty- if <- @ .loyaltyREV <- @ .morale-- then; <- @ .morale++
;

cyoa-choice: common2-cboard-help
	($ Thegroup .. " saves the man from all the cardboard boxes pinning him down. He can't spare a reward, but is really grateful!" $) text! ' cboard-help-check foreach-team-char goodfx
;

cyoa-choice: common2-cboard-bandit
	($ cname .. " steals the helpless man's " .. cboardprize rbold .. " and then takes the rest of his supplies!" $) text! badfx
	rob-guilt	
	($ "Got a " rbold .. cboardprize rbold $) +crtext cboardget trunk.weapon+ 
	10 20 rnd trunk-bullet+ 5 15 rnd trunk-rifle-shell+ 3 12 rnd trunk-shotgun-shell+
	2 4 rnd trunk-medical+ 1 6 rnd trunk-food+
	aholeREV
;

cyoa-bridge: common2-cboard-steal4
	someicon
	"Who should steal his weapon?" cyoa-text!
	' common2-cboard-steal cyoa-team-choice
;

cyoa: common2-cboard
	"Helping out someone in need!" cyoa-title!
	1 6 rnd to common2-cboardtype
	($ "While scavenging, " .. thegroup .. " runs into a man covered in cardboard boxes. He says he's been stuck there for days! He has a " .. cboardprize rbold .. " lying on the ground outside of his reach." $) text!
	choice( "Help him up" )choice: common2-cboard-help
	most-ahole if
		charchoice( ": Steal all his supplies" )choice: common2-cboard-bandit
		team-count 2 > if
			choice( "Choose a thief" )choice: common2-cboard-steal4
		then;
	then	
		
	solo if
		soloer choosechar choice( "Steal his weapon" )choice: common2-cboard-steal
	else
		"Should someone steal his weapon?" +crtext
		team-count 3 > if
			choice( "Choose a thief" )choice: common2-cboard-steal4
		then 
		' common2-cboard-steal 3 #cyoa-team-choice
	then
;

// Quick template for the usual "pick someone else" choice
(*

cyoa-bridge: 4
	someicon
	"Who should ?" cyoa-text!
	'  cyoa-team-choice
;

		team-count 3 > if
			'  2 #cyoa-team-choice
			choice( "Pick someone else" )choice: 4
		else

		then

*)

// Farm help

: common2-farm-reward
	8 trunk-food+
	most-charming if
		choosechar ($ cname .. " convinces the farmers to give " .. chim .. " a pitchfork!" $) +crtext
		1 WEAPON_PITCHFORK trunk.weapon+
	then
;

cyoa-choice: common2-farm-mechanic
	($ cname .. " offers " .. chis .. " services in fixing things around the farm. " $) text!
	cmechanic+ if	
		($ cHe .. " is so skilled that the large amount of work isn't a huge problem." $) +text
	else
		($ "There's so many broken things that " .. che .. " becomes exhausted." $) +text cchar tired
	then
	mechanicREV mechanic++
	cchar .mechanical 3 < if
		($ "The farmers refuse to give a reward after they find out everything is still broken." $) +crtext badfx
	else
		($ "The farmers are grateful for the work!" $) +crtext goodfx common2-farm-reward
	then
;

cyoa-choice: common2-farm-bandit
	($ cname .. " robs the peaceful farmers. They were completely caught by surprise against a non-zombie threat." $) text! badfx
	14 trunk-food+ rob-guilt2 aholeREV
;

cyoa-choice: common2-farm-haul
	someicon
	($ Thegroup .. " hauls tons of things around. There's always stuff to haul around on a farm. It's exhausting work, and " .. gthey .. " get" .. g1s .. " very tired!" $) text! goodfx 
	' .strengthREV foreach-team-char ' .strength++ foreach-team-char common2-farm-reward
	// added STR check for scythe
	strongest-pass if strongest choosechar
		($ "The farmers noticed " .. cname .. "'s unusual strength and gave " .. chim .. " a scythe as a gift!" $) +crtext
		1 WEAPON_SCYTHE trunk.weapon+
	then
	nosleep
;

cyoa-choice: common2-farm-till
	someicon
	($ Thegroup .. " helps with tilling the fields and planting new crops. It's exhausting work, and " .. gthey .. " get" .. g1s .. " very tired!" $) text! goodfx 
	' .fitnessREV foreach-team-char ' .fitness++ foreach-team-char common2-farm-reward
	// added FIT check for scythe
	fittest-pass if fittest choosechar
		($ "The farmers noticed " .. cname .. "'s level of fitness and gave " .. chim .. " a scythe as a gift!" $) +crtext
		1 WEAPON_SCYTHE trunk.weapon+
	then
	nosleep
;

cyoa: common2-farm
	"A Trip to the Farm" cyoa-title!
	($ Thegroup .. " runs into some people that are attempting to run a small farm. They offer 8 food in exchange for some help." $) text!
		choice( "Tilling and planting" )choice: common2-farm-till
		choice( "Hauling and hauling" )choice: common2-farm-haul
		most-ahole if
			charchoice( ": Rob the farm" )choice: common2-farm-bandit
		then
		mechanicest	charchoice( ": Repair work" )choice: common2-farm-mechanic
		choice( "No time to help" )choice
;

// Annoying diplomat

: common2-detoured
	vehicle.which@ 0 > if choice( "Take a detour" )choice: car-detour else
		// ($ Thegroup .. " takes a long detour on foot." $) +crtext 
		1 calendar-day+ +cr fullcamp
	then
;

cyoa-bridge: common2-meeting-talk
	($ cname .. " talks to the annoying representative." $) text!
	cpet? if
		($ cHe .. " isn't supposed to be capable of speech, so " .. che .. " mostly just licks " .. chim .. "self. The dingus is really insulted by being made to talk to a " .. cdogcat .. ", but the guards are amused. They let " .. thegroup .. " through!" $) +crtext goodfx morale++all
	then;
	cchar grating? if
		($ cHe .. " really gets along! They mostly exchange " .. "COOL IT" pbold .. " quips with each other. For an hour. " .. Thegroup .. " is let through." $) +crtext witsattREV goodfx cool-it++ cool-it++
	then;
	cchar oblivious? if
		($ cHe .. " somehow doesn't notice all the times the dingus is annoying or insulting. The meeting goes well and " .. thegroup .. " is let past." $) +crtext witsattREV goodfx
	then;
	cattitude+ if
		($ cHe .. " manages to be very polite. After enough pointless questioning, " .. thegroup .. " is let through the barricade." $) +crtext attitudeREV goodfx
	then;
	cchar angrystrong? if
		($ cHe .. " loses " .. chis .. " temper right away and knocks him out in one punch. The barricade guards are impressed by this! One gives a thumbs up. They decide to let " .. thegroup .. " through, anyway." $) +crtext attitudeREV strengthREV goodfx morale++all
	then;
	ctemper- if
		($ cHe .. " loses " .. chis .. " temper right away and starts screaming. This really spooks the guards, and they start shooting. " .. Thegroup .. " has to flee!" $) +crtext temperREV bitefx sting--all
		common2-detoured	
	then;
	ctemper+ temperroll or if
		($ cHe .. " manages to control " .. chis .. " temper! " .. Thegroup .. " is let through the barricade after some further annoyance." $) +crtext goodfx
	else
		($ cHe .. " tries to control " .. chis .. " temper, but the guy is just too annoying. He runs and tells the guards to not open the door. " .. Thegroup .. " has to find a way around!" $) +crtext badfx
		common2-detoured
	then temperREV
;

cyoa-bridge: common2-meeting-talk4
	someicon
	"Who should try to deal with the annoying person?" cyoa-text!
	' common2-meeting-talk cyoa-team-choice
;

cyoa: common2-meeting
	"Cool-it Diplomacy" cyoa-title!
	($ Thegroup .. " is blocked by another survivor group that set up a barricade to protect themselves. They are distrusting and heavily armed.
	The person they send to talk is " .. "EXTREMELY ANNOYING" rbold .. "." $) text!
		solo if
			($ " He keeps saying things like " .. "COOL IT" pbold .. " and " .. "SAY IT DON'T SPRAY IT." pbold $) +text
			soloer choosechar choice( "Try to not lose temper" )choice: common2-meeting-talk
		else
			" Who should try to deal with him?" +text
			team-count 3 > if
				' common2-meeting-talk 2 #cyoa-team-choice
				choice( "Pick someone else" )choice: common2-meeting-talk4
			else
				' common2-meeting-talk 3 #cyoa-team-choice
			then
		then
		vehicle.which@ 0 > if smartest charchoice( ": Take a detour" )choice: car-detour then
;

// Park Chatting

: common2-parkchat-tipme args( @ )
	<- @ 40 %chance if 
		.shooting++ 
	else
		50 %chance if 
			.mechanic++
		else
			.medical++
		then
	then
;

cyoa-choice: common2-parkchat-tips
	cpet? if 
		($ cname .. " " .. cbarkmeow .. "s in a very wise and experienced way." $) text!
	else
		($ cname .. " exchanges advice on different topics." $) text!
	then
	cwits- if
		"The friendly survivor is confused by the worthless advice." +crtext badfx witsREV
	then;
	cwits+ if
		"The friendly survivor is enlightened by the advice, and gives some great tips of his own!" +crtext
		goodfx ' common2-parkchat-tipme foreach-team-char witsREV
	then;
		($ "The friendly survivor appreciates it, and gives a little bit of advice to " .. cname .. " in return." $) +crtext
		cchar common2-parkchat-tipme goodfx witsREV
;

cyoa-choice: common2-parkchat-pep
	cpet? if 
		($ cname .. " " .. cbarkmeow .. "s in a reassuring way." $) text!
	else
		($ cname .. " tries to give a pep talk." $) text!
	then
	cattitude- if
		"The friendly survivor actually feels worse now." +crtext badfx attitudeREV
	then;
	cattitude+ if
		"The friendly survivor feels very optimistic after the talk, and gives an inspiring speech!" +crtext
		morale2+all goodfx attitudeREV
	then;
		($ "The friendly survivor gives some reassuring words in return." $) +crtext
		morale++all goodfx attitudeREV
;

cyoa-choice: common2-parkchat-bandit
	($ cname .. " easily robs the friendly person. He didn't have much, but that will teach him an important lesson in trusting others." $) text! badfx
		2 4 rnd trunk-food+ 50 %chance if 1 3 rnd trunk-medical+ then rob-guilt
		aholeREV
;

cyoa-bridge: common2-parkchat-coolit
	cpet? if
		($ cname .. " starts " .. "FARTING" rbold .. " at the person, and somehow continues to do so for several minutes without stopping for even a moment." $)
	else
		($ cname .. " interrupts the friendly survivor mid-sentence, to tell him to " .. "COOL IT." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Friendly Guy" recruitee .name!
		"He is awestruck by this. He claims that this is all according to the prophesies, and that he's been holding on to a gift for the Chosen One!" +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "He stares at " .. cname .. ", without speaking, until " .. thegroup .. " leaves." $) +crtext
		morale--all badfx
	then witsattREV 
;

cyoa: common2-parkchat
	"Like a Normal day" cyoa-title!
	($ Thegroup .. " meets another person just chilling on a park bench. He seems friendly. They have a conversation, just like before the world ended.
	What was the main topic?" $) text!
		smartest charchoice( ": Advice exchange" )choice: common2-parkchat-tips
		gallant charchoice( ": Pep Talk" )choice: common2-parkchat-pep
		most-ahole if
			charchoice( ": Giving me your stuff" )choice: common2-parkchat-bandit
		then
		most-grating if
			charchoice( ": Cooling It" fcoolit )choice: common2-parkchat-coolit
		then
;

// Busted leg

cyoa-choice: common2-busted-med
	($ cname .. " tries to treat the woman's busted leg." $) text!
	cmedical+ if
		($ cHe .. " uses " .. chis .. " medical expertise to quickly fix up her leg! She appreciates the help and gives what she can spare!" $) +crtext morale++ medicalREV medical++ goodfx -1 trunk-medical+ 2 trunk-food+
	then;
	cmedical- if
		($ cHe .. " has no idea what " .. che .. " is doing, wastes a day, and maybe makes things a little worse." $) +crtext medicalREV cloyalty- not if morale-- then loyaltyREV +cr fullcamp badfx -1 trunk-medical+ 1 calendar-day+
	then;
	medicalroll if
		($ cHe .. " takes a day to help the woman treat her leg. She is thankful, but can't spare much!" $) +crtext morale++ medicalREV medical++ goodfx 2 trunk-food+
	else
		($ cHe .. " tries to help over the course of a day, but doesn't manage to do much. Still, practice makes perfect." $) +crtext medicalREV medical++ cloyalty- not if morale-- then loyaltyREV badfx
	then +cr fullcamp -1 trunk-medical+ 1 calendar-day+ heal-event
;

cyoa-bridge: common2-busted-coolit
	cpet? if
		($ cname .. " starts rolling around on the cold ground." $)
	else
		($ cname .. " tells the woman to " .. "COOL IT." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Crutch Woman" recruitee .name!
		($ "She takes this as advice that she should apply cold compresses to her leg. After " .. thegroup .. " helps her with that, she offers a reward!" $) +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "She asks " .. "WHAT IS WRONG WITH YOU, EXACTLY?" pbold .. " before limping away in disgust." $) +crtext
		morale--all badfx
	then witsattREV
;

cyoa-choice: common2-busted-bandit
	($ cname .. " easily robs the wounded woman of what little supplies she had left. Way to go!" $) text! badfx
		2 4 rnd trunk-food+ rob-guilt aholeREV
;

cyoa: common2-busted
	"Busted Leg" cyoa-title!
	($ Thegroup .. " meets a woman limping around with a crutch. One of her legs was wounded in a recent fall as she was getting away from zombies." $) text!
		trunk-medical 0 > if
			medicalest charchoice( ": Help her leg" )choice: common2-busted-med
			choice( "No time to help" )choice
		else
			($ Thegroup .. " lacks any medical supplies to help her with." $) +crtext
			choice( "Leave her alone" )choice
		then
		most-grating if
			charchoice( ": Tell her to COOL IT" fcoolit )choice: common2-busted-coolit
		then
		most-ahole if
			charchoice( ": Steal her supplies" )choice: common2-busted-bandit
		then
;

// Car trouble

: common2-carhelp-win
	homesearch1 ammosearch1
;

cyoa-choice: common2-carhelp-mech
	($ cname .. " tries to fix the man's broken car." $) text!
	cmechanic+ if
		($ cHe .. " uses " .. chis .. " car knowledge to quickly repair it! He appreciates the help and gives some supplies!" $) +crtext morale++ mechanicREV mechanic++ goodfx ammosearch1
	then;
	cmechanic- if
		($ cHe .. " also has no idea what " .. che .. " is doing, and wastes a good day farting around." $) +crtext mechanicREV cloyalty- not if morale-- then loyaltyREV +cr fullcamp badfx 1 calendar-day+ heal-event
	then;
	mechanicroll if
		($ cHe .. " takes a day to help fix the man's car. He gives some supplies as a reward!" $) +crtext morale++ mechanicREV mechanic++ goodfx ammosearch1
	else
		($ cHe .. " tries to help over the course of a day, but isn't successful. Still, it's good practice." $) +crtext mechanicREV mechanic++ cloyalty- not if morale-- then loyaltyREV badfx
	then +cr fullcamp 1 calendar-day+ heal-event
;

cyoa-bridge: common2-carhelp-coolit
	cpet? if
		($ cname .. " ignores the stranded man and starts eating ants crawling on the cold ground." $)
	else
		($ cname .. " tells the stranded man to " .. "COOL IT." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Stranded Man" recruitee .name!
		($ "YOU'RE RIGHT! THE CAR JUST NEEDS MORE COOLANT TO RUN AGAIN!" pbold .. " He offers a reward!" $) +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "He starts yelling at " .. thegroup .. " until " .. gthey .. " leave" .. g1s .. "." $) +crtext
		morale--all badfx
	then witsattREV
;

cyoa-choice: common2-carhelp-bandit
	($ cname .. " easily robs the stranded man of his supplies, even his wrench." $) text! badfx
		common2-carhelp-win 1 WEAPON_WRENCH trunk.weapon+ rob-guilt aholeREV
;

cyoa: common2-carhelp
	"Helping with Car Trouble" cyoa-title!
	char-gen to recruitee 
	0 recruitee .mechanical!
	0 recruitee .female! ricon
	WEAPON_WRENCH recruitee .weapona!
	($ Thegroup .. " meets a man trying to repair his broken car. He clearly has no idea of what he's doing." $) text!
		mechanicest charchoice( ": Help fix car" )choice: common2-carhelp-mech
		most-charming if
			charchoice( ": Recruit him" )choice: recruit-yes
		then
		most-grating if
			charchoice( ": Tell him to COOL IT" fcoolit )choice: common2-carhelp-coolit
		then
		most-ahole if
			charchoice( ": Steal his supplies" )choice: common2-carhelp-bandit
		then
		choice( "No time to help" )choice
;

// Bandit raid

: common2-raid-prize
	1 WEAPON_RIFLE trunk.weapon+ 20 trunk-rifle-shell+ 15 trunk-food+ morale++all goodfx
;

cyoa-choice: common2-raid-calmshot
	($ cname .. " takes a cowboy rifle from the settlers and hides in a tall place. When the bandits show up, " .. che .. " makes multiple amazing shots to blow out their tires.
	The bandits become too freaked out to continue the attack! The fight is over before it starts. The settlers are glad to give the reward!" $) text! common2-raid-prize
;

: common2-raid-shoot+
	dup humany? 50 %chance and if .shooting++ then
;

: common2-raid-fightprize
	choosehuman alive? if
		"The settlers remain true to their word and give a large reward!" +crtext
		common2-raid-prize
		most-calmshot if
			choosechar ($ cname .. " does some crazy shots that inspire the others." $) +crtext
			' .shooting++ foreach-team-char
		else
			' common2-raid-shoot+ foreach-team-char
		then
	then
;

cyoa-choice: common2-raid-fight
	someicon ($ Thegroup .. " decides to help the settlers fight the bandits, directly." $) text!
	shootingREVall
	shooting-total 14 > if
		"The shooting skill of the group helps the settlers overwhelm the bandits. No one is hurt!" +crtext
		common2-raid-fightprize
	then;
	shooting-total 10 > if
		"The fight is harsh, but the group's shooting abilities save the settlement." +crtext
		ouch--all bitefx common2-raid-fightprize
	then;
	shooting-total 6 > if
		"The fight is brutal! The group's shooting skills barely help drive the bandits away." +crtext
		ouch12rnd--all bitefx common2-raid-fightprize
	then;
		"The help provided wasn't nearly enough in the shootout. The bandits drive everyone from the settlement." +crtext ouch2-all bitefx
;

cyoa-choice: common2-raid-mech
	($ cname .. " tries to build up the settlement's defenses." $) text!
	cmechanic+ if
		($ cHe .. " quickly makes plans and gets people to help " .. chim .. " turn the place into a fortress! The bandits are overwhelmed by the new defenses when they show up, and flee without hurting anyone." $) +crtext mechanicREV mechanic++ common2-raid-prize
	then;
	mechanicroll if
		($ cHe .. " tells the settlers how to build fortifications in some weak areas. The extra defenses seem to help a lot in the bandit attack! The settlers are thankful for the help!" $) +crtext mechanicREV mechanic++ common2-raid-prize
	else
		($ cHe .. " tries to fortify some things, but the efforts didn't seem to help much against the vicious bandit attack. The wounded settlers accuse " .. cname .. " of wasting their time, and refuse to give the reward." $) +crtext mechanicREV morale--all sting--all bitefx
	then
;

cyoa: common2-raid
	"Bandit Raid Defense" cyoa-title!
	($ Thegroup .. " is asked for help by a settlement. They say they will be raided by bandits soon. If " .. thegroup .. " helps with the defense, " .. gthey .. " will get a reward of 15 food, some ammo, and a cowboy rifle!
	This could be " .. "very dangerous..." rbold $) text!
		vehicle.which@ 0 > if 
			choice( "Don't help, drive off" )choice else choice( "Don't help them" )choice
		then
		choice( "Help them in shootout" )choice: common2-raid-fight
		mechanicest charchoice( ": Build up defenses" )choice: common2-raid-mech
		most-calmshot if
			charchoice( ": I'll handle it" )choice: common2-raid-calmshot
		then
;

// Rooftop relaxing

cyoa-choice: common2-relax-morale
	($ Thegroup .. " spends some time " .. "just chillin'" fbold .. "." $) text!
	morale2+all cool-it++ goodfx
;

cyoa-choice: common2-relax-strength
	($ Thegroup .. " gets some impromptu strength exercises in. The heaviest furniture that can be lifted is picked up, then set down. Repeat until " .. "MEGABUFF" fbold .. "." $) text!
	' .strength++ foreach-team-char goodfx
;

cyoa-choice: common2-relax-fitness
	($ Thegroup .. " gets some impromptu cardiovascular exercises in. Mostly burpees, some jumping jacks. Repeat until " .. "ULTRA FIT" fbold .. "." $) text!
	' .fitness++ foreach-team-char goodfx
;

cyoa-choice: common2-relax-med
	($ cname .. " teaches " .. everyone .. " what " .. che .. " knows about emergency medical care and semi-painless dentistry." $) text!
		cmedical- if 
			($ cHe .. " doesn't know much about it! It's a completely useless lesson." $) +crtext badfx
		else
			cchar ' .medical++ foreach-team-char-except goodfx
		then medicalREV morale++
;

cyoa-choice: common2-relax-mech
	($ cname .. " teaches " .. everyone .. " what " .. che .. " knows about car repair and other ways to accidentally mess up machinery." $) text!
		cmechanic- if 
			($ cHe .. " doesn't know much about it! It's a completely useless lesson." $) +crtext badfx
		else
			cchar ' .mechanic++ foreach-team-char-except goodfx
		then mechanicREV morale++
;

cyoa-choice: common2-roofrelax-reveal
	($ cname .. " reveals " .. chis .. " TRUE ESSENCE." $) text!
		allREV goodfx
;

cyoa: common2-roofrelax
	image-tired-survivor 63 to cyoaicon
	($ Thegroup .. " gets a rare peaceful moment to relax on an apartment roof." $) text!
	solo if
		soloer choosechar
		($ "With no one to bother " .. chim .. ", " .. che .. " decides to:" $) +crtext
		choice( "COOL IT" )choice: common2-relax-morale
		choice( "Exercise for strength" )choice: common2-relax-strength
		choice( "Exercise for fitness" )choice: common2-relax-fitness
	else
		"There's time to talk, and someone reveals a lot about themself:" +crtext
		' common2-roofrelax-reveal cyoa-team-choice
	then
;

// Safehouse relaxing

cyoa: common2-saferelax
	image-door-on-brick 27 to cyoaicon
	($ Thegroup .. " gets a peaceful moment to rest in a safehouse, no zombies in sight." $) text!
	solo if
		soloer choosechar
		($ "With no one to bother " .. chim .. ", " .. che .. " decides to:" $) +crtext
		choice( "COOL IT" )choice: common2-relax-morale
		choice( "Exercise for strength" )choice: common2-relax-strength
		choice( "Exercise for fitness" )choice: common2-relax-fitness
	else
		"With some spare time, they decide to:" +crtext
			50 %chance if 
				choice( "COOL IT" )choice: common2-relax-morale
			else
				50 %chance if
					choice( "Exercise for strength" )choice: common2-relax-strength
				else
					choice( "Exercise for fitness" )choice: common2-relax-fitness
				then
			then
			mechanicest charchoice( ": Teach mechanic skills" )choice: common2-relax-mech
			medicalest charchoice( ": Teach medical skills" )choice: common2-relax-med
	then
;

} }
