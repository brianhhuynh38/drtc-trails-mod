
cyoa{ road{

cyoa-choice: camp1-boring-run
	chumany? if 
		($ cname .. " decides that the spot looks TOO safe and " .. ghe .. " ends up PEELING OUT of there, fast, without eating or sleeping.
		" .. cname .. "'s feels smug in knowing " .. che .. " avoided that threat." $)
	else
		($ cname .. " decides that there's DANGER AFOOT so " .. che .. " starts barking at nothing and then never, ever stops until well into the next morning." $)
	then cyoa-text! 
	cchar ' .morale-- foreach-team-char-except morale++ witsattREV attitude-- nosleep badfx ;
cyoa-choice: camp1-boring-eat
	($ Thegroup .. " sets up camp in this boring but safe looking spot.
	" $) cyoa-text! fullcamp ;
cyoa: camp1-boring
	camp-pic
	"Boring Stop" cyoa-title! cyoaicon
	($ Thegroup .. " finds an empty spot along the road that looks as good for camping as any.
	" $) cyoa-text! basic-tone
		most-paranoid if
				"The weather is clear and there's no sign of danger in any direction." +text
				choice( "Eat and Rest" )choice: camp1-boring-eat
				charchoice( ": Can't fool me!" )choice: camp1-boring-run
		then;
		fullcamp
;

: campstoryreveal dup .temperREV dup .attitudeREV dup .witsREV dup .loyaltyREV ;
cyoa-choice: camp1-story-tell
	solo if
	($ soloname .. " swaps stories with pals and it's a very morale-boosting team-building experience despite everyone else being imaginary.
	" .. soloname .. " forgets all about the marshmallows, and they get left behind!" $) cyoa-text! soloer .morale3+ +cr fullcamp then;
	($ Thegroup .. " swaps stories about themselves. They learn a lot about each other, but forget all about the marshmallows!" $) cyoa-text! 
	' campstoryreveal foreach-team-char +cr fullcamp ;
cyoa-choice: camp1-story-eat
	($ Thegroup .. " sets up camp, then eats a huge bag of marshmallows instead of eating dinner. There's even some left over!" $) cyoa-text! 2 LOOT_FOOD trunk.loot+ +cr sleep goodfx ;
cyoa: camp1-story
	camp-pic
	"Old Campground" cyoa-title! cyoaicon
	someone choosechar
	($ Thegroup .. " finds an abandoned campground with a fire pit. " .. cname-He .. " even finds an old, somewhat stale extra-large pack of marshmallows!
	" $) cyoa-text! basic-tone
				choice( "Eat Marshmallows" )choice: camp1-story-eat
				choice( "Tell Stories" )choice: camp1-story-tell
;

cyoa: camp1-quick
	camp-pic
	"Quick Stop" cyoa-title! cyoaicon
	($ "After a long drive, " .. thegroup .. " finds a safe place to camp. The night passes with no incidents.
	" $) cyoa-text! basic-tone fullcamp
;

cyoa: camp1-passout
	"Passing Out" cyoa-title! cyoaicon
	($ "Exhausted after an extra long drive, " .. thegroup .. " scouts out a camping spot and falls asleep immediately after.
	" $) cyoa-text! basic-tone fullcamp
;

cyoa-choice: camp1-gas-no
	"Darn It" cyoa-title!
	($ " Next morning, " .. thegroup .. " goes to siphon the cars, but notices a large horde of zombies approaching and the gas has to be abandoned.
	" $) cyoa-text! fullcamp
;
cyoa-choice: camp1-gas-get
	"Gassy Night" cyoa-title!
	cpet? if
		($ cname .. " tries to siphon some gas, but finds that " .. chis .. " body structure is really poorly suited for it." $) cyoa-text! morale-- sleep
	else
		($ cname .. " siphons the gas, but accidentally swallows a bit and gets really, really sick that night instead of sleeping." $) cyoa-text! 40 70 rnd trunk-gas+ sleep-except morale2-
	then	
	+cr chowtime 
;

cyoa-bridge: camp1-gas-get4
	someicon
	"Who should siphon out the gas?" cyoa-text!
	' camp1-gas-get cyoa-team-choice
;

cyoa: camp1-gas
	image-abandoned-cars
	"Night Siphon" cyoa-title! cyoaicon
	($ Thegroup .. " sets up camp late. On the nearby highway is a bunch of abandoned cars. They're broken, but many should have a little bit of gas.
	Would you like to send someone to siphon out the gas?" $) cyoa-text! basic-tone
		team-count 3 > if
			' camp1-gas-get 2 #cyoa-team-choice
			choice( "Pick someone else" )choice: camp1-gas-get4
		else
			' camp1-gas-get 3 #cyoa-team-choice
		then
		choice( "Wait until morning" )choice: camp1-gas-no			
;

} }


