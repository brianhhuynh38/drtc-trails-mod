uses base.df
uses charfilt.df
uses charstats.df
uses road.df

// Covers dogs but also other pets. Non-dog pets are rare enough where they really don't need their own despair events.

cyoa{ road{

cyoa: dog-lost
	"Lost to Despair" cyoa-title!
	despair-start!
	($ cname .. " has lost all faith in " .. chis .. " pack. " .. cHe .. " sits down and simply refuses to continue on the journey." $) cyoa-text! badfx
		
	notsolo ccat? and if
		notcchar to victim
		($ cname .. " decides to maul " .. vname .. " real quick before giving up. " .. "One for the road." rbold $) +crtext
		victim .ouch2- bitefx
	then
	
	decruit-bad-silent-takeweapons
;
	
cyoa: dog-attack
	despair-start-victim!
	($ cDogcat .. " Attack" $) cyoa-title!
	($ cname .. " snaps, seemingly out of nowhere, and mauls " .. vname .. " in a flurry of " .. csnarlhiss .. "ing and " .. cteethclaws .. "." $) cyoa-text! 
	cdog? if 
		victim .ouch2- 
	else 
		ccat? if
			victim .kill
		else
			victim .ouch-- 
		then
	then bitefx
	ccat? if
		($ "The " .. cdogcat .. " doesn't seem guilty about this murder. In fact, " .. che .. " seems very pleased with " .. chim .. "self.
		" $) +crtext
		morale4+
	else	
		($ "Realizing what " .. che .. " did, the " .. cdogcat .. " flees and never returns." $) +crtext
		decruit-bad-silent-takeweapons
	then	
;

cyoa: dog-tantrum
	despair-start!
	($ cDogcat .. " Tantrum" $) cyoa-title!
	($ cname .. " throws a huge tantrum. " .. cHe .. " snorts and leaps around in one place. After finishing by ripping up some grass and peeing defiantly, " .. che .. " feels a little better." $) cyoa-text! goodfx
	morale++
	notsolo if
		ccat? if
			($ "During the tantrum, " .. cname .. " took the opportunity to maim " .. everyone .. "." $) +crtext
			cchar ' .sting-- foreach-team-char-except bitefx
		else
			($ "These antics were amusing to watch." $) +crtext
			cchar ' .morale++ foreach-team-char-except
		then
	then
;

} }


