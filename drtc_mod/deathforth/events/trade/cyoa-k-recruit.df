cyoa{ road{

cyoa-bridge: recruit1-yes
	ricon team-count 3 > if
	($ "YOU GOT LIKE A BILLION PEOPLE.
	GO AWAY.
	" pbold .. "You'll have to clear a spot if you want this annoying person." $) cyoa-text!	badfx recruit-replace?
	then;
	recruitee character# cyoa-char-icon!
	($ "FINE, whatever! 
	" pbold .. "The door opens and " .. rname .. " comes out." $) cyoa-text! 2 300 tone
		recruited recruitee .attitudeREV recruitee .witsREV judgerecruit
;
cyoa-choice: recruit1-no 
	($ "YOU ARE AN ESPECIALLY HUGE DINK. 
	YOU DINK.
	" pbold .. cname .. " is infuriated and smashes on the door for awhile. " .. Thegroup .. " ends up leaving shortly after." $) cyoa-text! badfx temperREV temper-- attitudeREV attitude--
;
cyoa-bridge: recruit1-coolit
	cpet? if
		($ cname .. " farts on the locked door." $)
	else
		($ cname .. " tells the voice to " .. "COOL IT." pbold $)
	then cyoa-text!
	cool-it-7 if
		($ "HHHHHHEH! YOU'RE ALRIGHT! " rbold $) +crtext
		team-count 3 > if
			($ "YOU HAVE TOO MANY PEOPLE, THOUGH.
			" rbold .. "The door stays shut unless you get rid of someone." $) +crtext badfx
			recruit-replace?
		else
			($ "OK, I'M IN!
			" rbold .. "The door opens and " .. rname .. " comes out." $) +text goodfx
			recruited recruitee .attitudeREV recruitee .witsREV
		then
	else cool-it++
		($ "SAY IT, DON'T SPRAY IT! 
		I HATE ANNOYING PEOPLE!
		" rbold .. "The voice refuses to open the door, and " .. thegroup .. " ends up leaving." $) badfx +crtext
	then attitudeREV witsREV
;

cyoa: recruit1
	"The Dinkdoor: Door of Dinks" cyoa-title!
	char-gen to recruitee
	0 1 rnd recruitee .attitude!
	0 1 rnd recruitee .wits!
	WEAPON_OTAKU recruitee .weapona!
	ricon
	($ Thegroup .. " comes across a house by the road. It's locked.
	While trying to smash the door down, a voice calls out:
	" .. "HEY LEAVE MY DOOR ALONE YOU DINKS" pbold $) cyoa-text! basic-tone
		choice( "Try to Recruit" )choice: recruit1-yes
		someone charchoice( ": I'M NOT A DINK" )choice: recruit1-no
		most-grating if 
			charchoice( " tells voice to COOL IT" fcoolit )choice: recruit1-coolit
		then
;

cyoa-choice: recruit2-yes
	ricon
	team-count 3 > if
		($ rname .. " says that there are too many people in your car already, and that " .. rhe .. "'ll be fine staying put for awhile longer.
		Ditch someone to make room:" $) cyoa-text!
		recruit-replace?
	then;
	($ "Sure, I'll join! I was running out of food, anyway.
	" pbold .. rname .. " is now part of the team!
	" .. rname .. " brings along some food supplies! Bonus!
	Onward to Canada!" $) cyoa-text! 2 300 tone
	6 LOOT_FOOD trunk.loot+ recruitee .recruit recruitee .temperREV recruitee .witsREV
	gstats{ ugnome-extraZP 2 > if 1 zombop+! then }
	judgerecruit
;

cyoa-choice: recruit2-rob
	solo if
	($ cname .. " robs " .. rname .. ".
	This is probably why your group has just one person in it." $) cyoa-text!
	else
	($ cname .. " convinces the group to rob " .. rname .. "." $) cyoa-text!
	cchar ' .morale-- foreach-team-char-except
	cchar ' .loyalty-- foreach-team-char-except
	then
	6 LOOT_FOOD trunk.loot+ loyaltyREV badfx
;
cyoa-choice: recruit2-coolit
	cpet? if
		($ cname .. " farts. It echoes in the grocery store." pbold $)
	else
		($ cname .. " tells " .. rname .. " to " .. "COOL IT." pbold $)
	then cyoa-text!
	cool-it-7 if
		($ rHe .. " is impressed by this devil-may-care attitude!" $) +crtext
		team-count 3 > if
			($ "Here, take a bunch of food I had stored up!
			" pbold .. "The team goes on their way with the food." $) +crtext goodfx 15 trunk-food+
		else
			($ "Let me come with you! I have a bunch of food!" pbold $) +crtext goodfx
			recruited recruitee .temperREV recruitee .witsREV 15 trunk-food+
			most-paranoid if recruitee .attitudeREV then
		then
	else cool-it++
		($ rname .. " is mostly just confused by this.
		" .. "What? I wasn't doing anything...
		" pbold .. " Wow! What a nerd! " .. Thegroup .. " leaves " .. rhim .. "." $) badfx +crtext
	then attitudeREV witsREV
;

cyoa: recruit2
	"Grocery Squatter" cyoa-title!
	char-gen to recruitee
	5 6 rnd recruitee .composure!
	5 6 rnd recruitee .wits!
	3 4 rnd recruitee .morale!
	50 %chance if 0 1 else 5 6 then rnd recruitee .attitude!
	WEAPON_FRYING recruitee .weapona!
	ricon
	($ Thegroup .. " is about to break into a small, fortified grocery store, but then finds " .. rname .. " already living inside!
	" .. rHe .. " seems like a calm, clever person." $) cyoa-text! basic-tone
		choice( "Ask " .. rname .. " to join" )choice: recruit2-yes
		choice( "Leave " .. rname .. " alone" )choice
		weaselest-pass if
			weaselest charchoice( ": Let's rob this dingus!" )choice: recruit2-rob
		then
		most-grating if 
			charchoice( " tells " .. rname .. " to COOL IT" fcoolit )choice: recruit2-coolit
		then
;

cyoa: recruit3
	"Kinda Pushy" cyoa-title!
	char-gen to recruitee
	recruitee character# cyoa-char-icon!
	0 1 rnd recruitee .composure!
	5 6 rnd recruitee .attitude!
	4 5 rnd recruitee .morale!
	4 recruitee 50 %chance if .strength! else .shooting! then
	WEAPON_BASEBALL recruitee .weapona!
	($ Thegroup .. " finds another traveller by the roadside, named " .. rname .. ".
	" .. rHe .. " insists on joining the group, immediately. " $) cyoa-text! basic-tone
	solo if
		($ thegroup .. " considers the benefits of " .. ghis .. " LONE WOLF strategy so far. Should " .. gthey .. " recruit " .. rname .. "?" $) +crtext
		recruit-yesno
	then;
	team-count 4 < if
		($ Thegroup .. " isn't even given a choice.
		" .. rname .. " joins the party! Onward to Canada?" $) +text
		recruitee .recruit recruitee .attitudeREV recruitee .temperREV
		gstats{ ugnome-extraZP 2 > if 1 zombop+! then }
		judgerecruit
	then;
		"The car is already full! Someone will have to be kicked out of the group to make room:" +text
		recruit-replace?
;

// Possible stat bonus recruit events

: recruit-intro
	char-gen to recruitee ricon
	($ Thegroup .. " finds a new person, " .. rname .. ".
	" .. rHe .. " is alone, and wants to join the team!" $) cyoa-text! basic-tone
;

: recruit-confirm
	choosechar
	($ cname .. 
	1 5 rnd case
		1 of " vouches for this claim." endof
		2 of " trusts this based on " .. chis .. " experience." endof
		3 of " thinks that " .. rhe .. " is telling the truth." endof
		4 of " has a good feeling about this person." endof
		5 of " has a gut feeling that this is the truth." endof
	endcase
	$) +crtext recruitee
;

: recruit-scoff
	choosechar
	($ cname .. 
	1 6 rnd case
		1 of " seriously doubts this." endof
		2 of " rolls " .. chis .. " eyes at this obvious lie." endof
		3 of " thinks that " .. rhe .. " is lying." endof
		4 of " has a very bad feeling about this person." endof
		5 of " has a gut feeling that this is a lie." endof
		6 of " knows that something is off about " .. rname .. "." endof
	endcase
	$) +crtext recruitee .loyaltyREV recruitee
;

: recruit-strength-flavor
	1 6 rnd case
		1 of " flexes in a dramatic way to prove it." endof
		2 of " talks about a past life as a lumberjack." endof
		3 of " talks about a former life as a bodybuilder." endof
		4 of " boasts about being a former 'caged pit deathmatch' fighter." endof
		5 of " bends a spoon, very aggressively." endof
		6 of " flexes and grunts HARD, beads of sweat forming on forehead." endof
	endcase
;

: recruit-fitness-flavor
	1 6 rnd case
		1 of " reveals that " .. rhe .. " was a track star in college." endof
		2 of " does a clumsy half-cartwheel. Maybe it's an off day for " .. rhim .. "?" endof
		3 of " shows off an elaborate stretching routine to prove it." endof
		4 of " jogs in place for a bit. It looks ok? There's little to no wheezing..." endof
		5 of " does a quick little dance. Then " .. rname .. " stops, saying to recruit " .. rhim .. " for more." endof
		6 of " boasts about how " .. rhe .. " used to be an olympic athlete." endof
	endcase
;

: recruit-shooting-flavor
	1 6 rnd case
		1 of " talks about " .. rhis .. " favorite guns, though it's worrying that " .. rhe .. " pronounces 'guns' wrong." endof
		2 of " talks about a past life as a police officer. 'Not just any cop, a supercop'." endof
		3 of " talks about all " .. rhis .. " shooting trophies and trophies for best guns." endof
		4 of " refuses to prove " .. rhe .. "'s a good shot because " .. rhis .. " finger hurts right now. Suspicious." endof
		5 of " twirls a pistol around, before accidentally dropping and breaking it." endof
		6 of " shoots a rubber band off " .. rhis .. " finger to show off " .. rhis .. " aim, but misses." endof
	endcase
;

: recruit-medical-flavor
	1 6 rnd case
		1 of " spouts a bunch of medicalish jargon. Seems plausible?" endof
		2 of " is evasive when asked what " .. rhis .. " practice was." endof
		3 of " nearly admits that " .. rhe .. " wasn't a 'doctor-doctor', but catches " .. rhim .. "self." endof
		4 of " talks about " .. rhis .. " long career in healthcare." endof
		5 of " says that " .. rhe .. " never had a degree, but learned a lot 'on the streets'." endof
		6 of " twirls a scalpel around like an expert, but then drops it into a storm drain." endof
	endcase
;

: recruit-mechanical-flavor
	1 6 rnd case
		1 of " discusses car repair, but its hard to tell if " .. rhe .. "'s bluffing or not." endof
		2 of " talks about how " .. rhe .. " used to own a repair shop." endof
		3 of " says " .. rhe .. " used to be an engineer, but is evasive about what kind." endof
		4 of " drones on about " .. rhis .. " lifelong passion for cars." endof
		5 of " promises that if something breaks, " .. rhe .. " can fix it!" endof
		6 of " smells like car oil, but maybe that's just poor hygiene." endof
	endcase
;

: recruit-highstat 80 %chance if 4 else 5 then recruitee ;
: recruit-liestat 0 1 rnd recruitee .loyalty! 0 2 rnd recruitee ;
		

cyoa: recruit-strength
	"Strong Stranger" cyoa-title!
	recruit-intro
	WEAPON_SLEDGE recruitee .weapona!
	($ rname .. " claims to be really strong. " .. rHe .. recruit-strength-flavor $) +crtext
	60 %chance if
		recruit-highstat .strength!
		strongest-pass if strongest recruit-confirm .strengthREV strengthREV
		else
			most-paranoid if recruit-confirm .strengthREV witsREV attitudeREV then 
		then
	else
		recruit-liestat .strength!
		strongest-pass if strongest recruit-scoff .strengthREV strengthREV
		else
			most-paranoid if recruit-scoff .strengthREV witsREV attitudeREV then
		then
	then
	recruit-yesno
;

cyoa: recruit-fitness
	"Recruitment of the Fittest" cyoa-title!
	recruit-intro
	WEAPON_HOCKEY recruitee .weapona!
	($ rname .. " claims to be physically fit. " .. rHe .. recruit-fitness-flavor $) +crtext
	60 %chance if
		recruit-highstat .fitness!
		fittest-pass if fittest recruit-confirm .fitnessREV fitnessREV
		else
			most-paranoid if recruit-confirm .fitnessREV witsREV attitudeREV then 
		then
	else
		recruit-liestat .fitness!
		fittest-pass if fittest recruit-scoff .fitnessREV fitnessREV
		else
			most-paranoid if recruit-scoff .fitnessREV witsREV attitudeREV then
		then
	then
	recruit-yesno
;
cyoa: recruit-shooting
	"A New Gun in Town" cyoa-title!
	recruit-intro
	WEAPON_PISTOL recruitee .weapona!
	($ rname .. " claims to be a great shot with a gun. " .. rHe .. recruit-shooting-flavor $) +crtext
	60 %chance if
		recruit-highstat .shooting!
		shootest-pass if shootest recruit-confirm .shootingREV shootingREV
		else
			most-paranoid if recruit-confirm .shootingREV witsREV attitudeREV then 
		then
	else
		recruit-liestat .shooting!
		shootest-pass if shootest recruit-scoff .shootingREV shootingREV
		else
			most-paranoid if recruit-scoff .shootingREV witsREV attitudeREV then
		then
	then
	
	recruit-yesno
;
cyoa: recruit-medical
	"Medical Assistance" cyoa-title!
	recruit-intro
	WEAPON_MACHETE1 recruitee .weapona!
	($ rname .. " claims to be experienced in medical matters. " .. rHe .. recruit-medical-flavor $) +crtext
	60 %chance if
		recruit-highstat .medical!
		medicalest-pass if medicalest recruit-confirm .medicalREV medicalREV
		else
			most-paranoid if recruit-confirm .medicalREV witsREV attitudeREV then 
		then
	else
		recruit-liestat .medical!
		medicalest-pass if medicalest recruit-scoff .medicalREV medicalREV
		else
			most-paranoid if recruit-scoff .medicalREV witsREV attitudeREV then
		then
	then	
	
	recruit-yesno
;
cyoa: recruit-mechanical
	"Mechanic Service" cyoa-title!
	recruit-intro
	WEAPON_WRENCH recruitee .weapona!
	($ rname .. " claims to be great at fixing cars and machinery. " .. rHe .. recruit-mechanical-flavor $) +crtext
	60 %chance if
		recruit-highstat .mechanical!
		mechanicest-pass if mechanicest recruit-confirm .mechanicREV mechanicREV
		else
			most-paranoid if recruit-confirm .mechanicREV witsREV attitudeREV then 
		then
	else
		recruit-liestat .mechanical!
		mechanicest-pass if mechanicest recruit-scoff .mechanicREV mechanicREV
		else
			most-paranoid if recruit-scoff .mechanicREV witsREV attitudeREV then
		then
	then
	recruit-yesno
;

cyoa: recruit-dog-feed
	ricon trunk-food 2 < if
		"There wasn't enough food to spare for the dog.
		It runs away when approached."  cyoa-text! badfx
	then;
	($ Thegroup .. " lures " .. rname .. " over with food. " .. rHe .. " knows a good source of meals when " .. rhe .. " sees it, and decides to stick around. " $) cyoa-text! 2 300 tone
	-2 trunk-food+
	team-count 3 > if
		($ "The group is full. Who would you like to replace to make room for " .. rname .. "?" $) +crtext
			' recruit-replace cyoa-team-choice
			choice( "Leave " .. rname .. " behind" )choice
	then;
		recruited
;

cyoa: recruit-dog
	"Dog Recruitment" cyoa-title!
	char-gen to recruitee ricon
	recruitee .doggify
	($ "There is a dog wandering nearby. It's keeping its distance, but watching " .. thegroup .. " with guarded interest.
	It's become rare to see a dog that trusts people." $) cyoa-text! basic-tone foodamt
		choice( "Recruit dog - 2 food" )choice: recruit-dog-feed
		choice( "Leave the dog alone" )choice
;

cyoa: recruit-dogpal
	"Dog Magnet!" cyoa-title!
	choosedogpal
	char-gen to recruitee ricon
	recruitee .doggify
	($ cname .. " is a lightning rod, but for dogs!
	A dog approaches " .. thegroup-him .. " out of nowhere. It seems eager to join up!" $) cyoa-text! basic-tone
		recruit-yesno
;

} }


