uses paul/region-layout.df

"../deathforth/rooms/duodenum.txt" $load-rooms

locdef{

location: traderlab
		max-rooms: 1
		entry-tags: traderlab-entry
		pre-script: set-theme-bunker
		script: set-theme-bunker
		post-script: 
		
		room-choice: traderlab-entry
			room-weight: 10
			room-count: 1
			room-script: ndx 
			room-post-script:
			filter-only

}

cyoa{

0 value tradelab-bluebot-sold

cyoa-choice: tradelab-bluebot-sale
	iicon ($ pname .. " buys a " .. "BLUE ROBIT" bbold .. "!
	Throw it on the ground and it will help you until it runs out of batteries."  $) text!
	goodfx 1 WEAPON_BLUEROBOT trunk.weapon+ -3 trunk-food+ ' tradelab-bluebot-sold ++
	tradelab-bluebot-sold 1 > if clear-cyoa then
;

cyoa-choice: tradelab-redbot-sale
	iicon ($ pname .. " buys a " .. "RED ROBIT" rbold .. "!
	Throw it on the ground and it will help you until it runs out of batteries."  $) text!
	goodfx 1 WEAPON_REDROBOT trunk.weapon+ -6 trunk-food+ clear-cyoa
;

cyoa: tradelab-bluebot
	iicon "Helpful Robot" cyoa-title!
	"It's a helpful robot!
	For its off time, it's programmed with a sales pitch." text!
	"1. BEEP 2. HONK
	3. I AM SELLING ROBITS
	4. UP TO TWO ROBITS" bbold +crtext
	choice( "No sale!" )choice
	trunk-food 2 > tradelab-bluebot-sold 2 < and if
		choice( "Buy Blue Robot - 3 food" )choice: tradelab-bluebot-sale
	else
		"It wants 3 food for one." +crtext
	then
;

cyoa: tradelab-redbot
	iicon "Menacing Robot" cyoa-title!
	"This robot seems intense.
	Its claws look menacing. It must be a guard." text!
	"1. BEEP 2. HONK
	3. I AM SELLING A ROBIT
	4. MAXIMUM OVERPOWER" rbold +crtext
	choice( "Get away from me" )choice
	trunk-food 5 > if
		choice( "Buy Red Robot - 6 food" )choice: tradelab-redbot-sale
	else
		"It wants 6 food." +crtext
	then
;

0 value labsci-doodad
0 value labsci-mazer

cyoa-choice: tradelab-mazer-sale
	iicon ($ pname .. " buys a " .. "MAZER PISTOL" rainbold .. "!
	" .. "Someone with better aim than me could really use this efficently.
	The piercing potential is immense due to the power of " pbold .. "Mazer Energy!" fbold $) text!
	goodfx 200 WEAPON_MAZER trunk.weapon+ -12 trunk-food+ ' labsci-mazer ++
;

cyoa-choice: tradelab-doodad-sale
	iicon ($ pname .. " buys a " .. "SCIENCY DOODAD" rainbold .. "!
	" .. "This can teleport you short distances. 
	It has 5 shots. Don't lose count, I forgot to build a useful display for it. The display on it now doesn't mean anything, it just blinks at random." pbold $) text!
	goodfx 5 WEAPON_DOODAD trunk.weapon+ -5 trunk-food+ ' labsci-doodad ++
;

cyoa-bridge: tradelab-recruit
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	20 recruit-yes-food "I'm ready to zap things! I brought my solar recharger." pbold +crtext 
	recruitee .mechanicREV recruitee .witsREV recruitee .shootingREV
;

cyoa-bridge: tradelab-recruit-warn
	"Sure, I can join you. I'm getting bored here, I need to do some research out in the field.
	Finish buying anything you want before you recruit me! It's not that I don't trust you, but after I join I'm locking all my weapons to my fingerprint ID. 
	Just in case I die mysteriously." pbold text!
		choice( "Recruit her later" )choice
		choice( "Recruit her! - 20 food" )choice: tradelab-recruit
;

cyoa: tradelab-scichat
	"The Science of Shopping" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"All trading you do here will go towards the advancement of science!" pbold text!
		trunk-food 5 < if
			"The scientist takes pity on your lack of food. She gives you a free teleporter, slightly used. 2 shots remaining!" +crtext clear-cyoa 2 WEAPON_DOODAD trunk.weapon+
		then;
		choice( "Leave her for now" )choice
		trunk-food 4 > labsci-doodad 1 < and if
			choice( "Buy Teleporter - 5 food" )choice: tradelab-doodad-sale
		then
		trunk-food 11 > labsci-mazer 1 < and if
			choice( "Buy Mazer Pistol - 12 food" )choice: tradelab-mazer-sale
		then
		trunk-food 19 > if
			labsci-doodad 1 < labsci-mazer 1 < and if
				choice( "Recruit her to team - 20 food" )choice: tradelab-recruit-warn
			else
				choice( "Recruit her to team! - 20 food" )choice: tradelab-recruit
			then
		then
;

}

roomgen{

: tradelab-bots 
	still-npc .redbotify 2 movey cyoa{ ' tradelab-redbot cyoa! }
	regiondef{ ' trader-event lt .events! 1 lt .timer! }
	still-npc .bluebotify -0.5 movey cyoa{ ' tradelab-bluebot cyoa! }
	regiondef{ ' trader-event lt .events! 1 lt .timer! }
;
: tradelab-scientist still-npc .madscify 
	cyoa{ ' tradelab-scichat cyoa! } 
	regiondef{ ' trader-event lt .events! 1 lt .timer! }
;

}

map{ regiondef{

trade-camp-reset

: bldg tanchor!
	"traderlab" $picklocid addloc prefab{ 7 bunker } 
;

0 value bfx
0 value bfy
0 value snx
0 value sny

: river tile@
	dup LAYER_FLOOR particles32_base_id 0x 10 + .lspriteid!
	dup LAYER_FLOOR 0 0 .tile_layer.off!
	dup LAYER_FLOOR 0 .tile_layer.flipx!

	dup LAYER_FLOORTOP particles32_base_id 0x 11 + .lspriteid!
	dup LAYER_FLOORTOP 0 0 .tile_layer.off!
	dup LAYER_FLOORTOP 0 .tile_layer.flipx!

	TILE_BLOCK over .id!
	drop
;

: riveredge
	2dup river
	tile@ LAYER_FLOORTOP particles32_base_id 0x 12 + .lspriteid!
;

: cliff 2dup blocked tile@
	dup LAYER_FLOORTOP 0 .tile_layer.flipx!
	LAYER_FLOORTOP street_tiles_base_id 0x a9 + 0 3 rnd + .lspriteid!
;

: cliffedge 2dup blocked tile@
	dup LAYER_FLOORTOP 0 .tile_layer.flipx!
	LAYER_FLOORTOP street_tiles_base_id 0x ba + 0 3 rnd + .lspriteid!
;

: bank 2dup tile@ args( x y t )
	<- t LAYER_WALLSIDE street_tiles_base_id 0x 99 + 0 3 rnd + .lspriteid!
	<- t LAYER_FLOORTOP particles32_base_id 0x 12 + .lspriteid!
	<- x <- y riveredge
;

: bushy >r tile@
	// dup LAYER_WALLSIDE rnd5050 .tile_layer.flipx!
	dup LAYER_WALLSIDE street_tiles_base_id 0x 89 + 0 3 rnd + .lspriteid!
	LAYER_WALLSIDE 0 r> .tile_layer.off!
;

' plotgrass >defer ft

: pt
	xyp.xy ft // Plot the current floor by default

	xyp.i char G = if ' plotgrass to ft xyp.xy ft then;
	xyp.i char x = if ' plotgravel to ft then;
	xyp.i char ^ = if xyp.xy river then;
	xyp.i char v = if xyp.xy riveredge then;
	xyp.i char \ = if xyp.xy cliff then;
	xyp.i char / = if xyp.xy cliffedge xyp.xy -4 -5 rnd bushy then;
	xyp.i char | = if xyp.xy bank then;
	xyp.i char o = if xyp.xy -8 -7 rnd bushy then;
	xyp.i char $ = if xyp.xy grassedge then;
	xyp.i char W = if trader-weapon-buyer then;

	xyp.i char t = if pictable1 lt-here 8 rdrift then;		
	xyp.i char @ = if xyp.xy bldg then;
	xyp.i char T = if traderhere then;
	xyp.i char Z = if tnomey-spawn then;
;

"G........................
.........................
.........................
.........................
...t.................t...
.........................
.........................
/////////////////////////
\\\\\\\\\\\\\\\\\\\\\\\\\
|||||||||||||||||||||||||
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
ooooooooooooooooooooooooo
.........................
.........................
....W...Z.@.........T....
........................x
$$$$$$$$$$$$$$$$$$$$$$$$$
.........................
.........................
.........................
.........................
.........................
........................."
constant maplayout

: plotmap
	0 -1 maplayout xyplot: pt
;

: site-setup
	1 no_time!
	7 14 rnd o'clock

	' plotgrass to ft

	30 %chance if 0 0.5 frnd weather! -1 2 frnd wind_force! else -1 1 frnd wind_force! then

	plotmap
	mission{ no-edge-spawns }

	// Put char and chars in middle of map
	9 %mv-up
	24 %chars-up
;

region: specialcamp-1
	w: maplayout layoutw
	h: maplayout layouth

	trade-mode

	post-script: site-setup

} }
